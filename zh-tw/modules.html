<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Strict//EN"
"http://www.w3.org/TR/xhtml1/DTD/xhtml1-strict.dtd">
<html xmlns="http://www.w3.org/1999/xhtml">
    <head>
        <meta http-equiv="Content-Type" content="text/html; charset=utf-8" />

        <meta property="og:title" content="Haskell 趣學指南" />
        <meta property="og:type" content="website" />
        <meta property="og:url" content="http://learnyouahaskell-zh-tw.csie.org" />
        <meta property="og:image" content="http://learnyouahaskell-zh-tw.csie.org/img/bird.png" />

        <style type="text/css">
            @import url(../css/style.css);
            @import url(../css/feedback.css);
        </style>
        <script type="text/javascript" src="../js/jquery.js"></script>
        <script type="text/javascript" src="../js/jquery.chili-2.2.js"></script>
        <script type="text/javascript" src="../js/script.js"></script>
        <script type="text/javascript" src="../js/html2canvas.js"></script>
        <script type="text/javascript" src="../js/jsfeedback.js"></script>
        <script type="text/javascript">
             ChiliBook.recipeFolder = "../js/chili/";  
             ChiliBook.automaticSelector = "pre";
        </script>
        <script type="text/javascript">
            $(function(){
                $('#feedback').click(function(){
                    $('body').feedback();
                })
            });
        </script>
        <script defer type="text/javascript">
            var _gaq = _gaq || [];
            _gaq.push(['_setAccount', 'UA-32830659-1']);
            _gaq.push(['_trackPageview']);

            (function() {
                var ga = document.createElement('script'); ga.type = 'text/javascript'; ga.async = true;
                ga.src = ('https:' == document.location.protocol ? 'https://ssl' : 'http://www') + '.google-analytics.com/ga.js';
                    var s = document.getElementsByTagName('script')[0]; s.parentNode.insertBefore(ga, s);
            })();
        </script>
        <title>模組 (Modules)</title>
    </head>
    <body>
        <div id="header">
        </div>

        <div id="main">
            <ul class="nav">
                <li class="left">  <img src="../img/prv.png" alt="prev" /><a href="high-order-function.html">高階函數</a></li>
                <li class="center"><a href="chapters.html">目录</a></li>
                <li class="right"> <a href="make-types-and-classes-for-ourselves.html">構造我們自己的 Types 和 Typeclasses</a><img src="../img/nxt.png" alt="next" /></li>
            </ul>
            <a name="模組_(Modules)"></a><h1>模組 (Modules)</h1>

<a name="裝載模組"></a><h2>裝載模組</h2>

<img src="../img/modules.png" alt="" style="float:right" class="floatright" />
<p>Haskell 中的模組是含有一組相關的函數，型別和型別類的組合。而 Haskell 程序的本質便是從主模組中引用其它模組並呼叫其中的函數來執行操作。這樣可以把程式碼分成多塊，只要一個模組足夠的獨立，它裡面的函數便可以被不同的程序反覆重用。這就讓不同的程式碼各司其職，提高了程式碼的健壯性。</p>

<p>Haskell 的標準庫就是一組模組，每個模組都含有一組功能相近或相關的函數和型別。有處理 List 的模組，有處理並發的模組，也有處理複數的模組，等等。目前為止我們談及的所有函數,型別以及型別類都是 <code>Prelude</code> 模組的一部分，它預設自動裝載。在本章，我們看一下幾個常用的模組，在開始瀏覽其中的函數之前，我們先得知道如何裝載模組.</p>

<p>在 Haskell中，裝載模組的語法為 <code>import</code>，這必須得在函數的定義之前，所以一般都是將它置於程式碼的頂部。無疑，一段程式碼中可以裝載很多模組，只要將 <code>import</code> 語句分行寫開即可。裝載 <code>Data.List</code> 試下，它裡面有很多實用的 List 處理函數.</p>

<p>執行 <code>import Data.List</code>，這樣一來 <code>Data.List</code> 中包含的所有函數就都進入了全局命名空間。也就是說，你可以在程式碼的任意位置呼叫這些函數.<code>Data.List</code> 模組中有個 <code>nub</code> 函數，它可以篩掉一個 List 中的所有重複元素。用點號將 <code>length</code> 和 <code>nub</code> 組合: <code>length . nub </code>，即可得到一個與 <code>(\xs -&gt; length (nub xs))</code> 等價的函數。</p>
<pre class="code">import Data.List  
  
numUniques :: (Eq a) =&gt; [a] -&gt; Int  
numUniques = length . nub</pre>
<p>你也可以在 ghci 中裝載模組，若要呼叫 <code>Data.List</code> 中的函數，就這樣:</p>
<pre class="code">ghci&gt; :m Data.List</pre>
<p>若要在 ghci 中裝載多個模組，不必多次 <code>:m</code> 命令，一下就可以全部搞定:</p>
<pre class="code">ghci&gt; :m Data.List Data.Map Data.Set</pre>
<p>而你的程序中若已經有包含的程式碼，就不必再用 <code>:m</code> 了.</p>

<p>如果你只用得到某模組的兩個函數，大可僅包含它倆。若僅裝載 <code>Data.List</code> 模組 <code>nub</code> 和 <code>sort</code>，就這樣:</p>
<pre class="code">import Data.List (nub，sort)</pre>
<p>也可以只包含除去某函數之外的其它函數，這在避免多個模組中函數的命名衝突很有用。假設我們的程式碼中已經有了一個叫做 <code>nub</code> 的函數，而裝入 <code>Data.List</code> 模組時就要把它裡面的 <code>nub</code> 除掉.</p>
<pre class="code">import Data.List hiding (nub)</pre>
<p>避免命名衝突還有個方法，便是 <code>qualified import</code>，<code>Data.Map</code> 模組提供一了一個按鍵索值的資料結構，它裡面有幾個和 <code>Prelude</code> 模組重名的函數。如 <code>filter</code> 和 <code>null</code>，裝入 <code>Data.Map</code> 模組之後再呼叫 <code>filter</code>，Haskell 就不知道它究竟是哪個函數。如下便是解決的方法:</p>
<pre class="code">import qualified Data.Map</pre>
<p>這樣一來，再呼叫 <code>Data.Map</code> 中的 <code>filter</code> 函數，就必須得 <code>Data.Map.filter</code>，而 <code>filter</code> 依然是為我們熟悉喜愛的樣子。但是要在每個函數前面都加 <code>個Data.Map</code> 實在是太煩人了! 那就給它起個別名，讓它短些:</p>
<pre class="code">import qualified Data.Map as M</pre>
<p>好，再呼叫 <code>Data.Map</code> 模組的 <code>filter</code> 函數的話僅需 <code>M.filter</code> 就行了</p>

<p>要瀏覽所有的標準庫模組，參考這個手冊。翻閲標準庫中的模組和函數是提升個人 Haskell 水平的重要途徑。你也可以各個模組的原始碼，這對 Haskell 的深入學習及掌握都是大有好處的.</p>

<p>檢索函數或搜尋函數位置就用 <a href="http://www.Haskell.org/hoogle/">Hoogle</a>，相當了不起的 Haskell 搜索引擎! 你可以用函數名，模組名甚至型別聲明來作為檢索的條件.</p>
<a name="Data.List"></a><h2>Data.List</h2>


<p>顯而易見，<code>Data.List</code> 是關於 List 操作的模組，它提供了一組非常有用的 List 處理函數。在前面我們已經見過了其中的幾個函數(如 <code>map</code> 和 <code>filter</code>)，這是 <code>Prelude</code> 模組出於方便起見，導出了幾個 <code>Data.List</code> 裡的函數。因為這幾個函數是直接引用自 <code>Data.List</code>，所以就無需使用 <code>qualified import</code>。在下面，我們來看看幾個以前沒見過的函數:</p>

<p><strong>intersperse</strong> 取一個元素與 List 作參數，並將該元素置於 List 中每對元素的中間。如下是個例子:</p>
<pre class="code">ghci&gt; intersperse '.' "MONKEY"  
"M.O.N.K.E.Y"  
ghci&gt; intersperse 0 [1,2,3,4,5,6]  
[1,0,2,0,3,0,4,0,5,0,6]</pre>
<p><strong>intercalate</strong> 取兩個 List 作參數。它會將第一個 List 交叉插入第二個 List 中間，並返回一個 List.</p>
<pre class="code">ghci&gt; intercalate " " ["hey","there","guys"]  
"hey there guys"  
ghci&gt; intercalate [0,0,0] [[1,2,3],[4,5,6],[7,8,9]]  
[1,2,3,0,0,0,4,5,6,0,0,0,7,8,9]</pre>
<p><strong>transpose</strong> 函數可以反轉一組 List 的 List。你若把一組 List 的 List 看作是個 2D 的矩陣，那 <code>transpose</code> 的操作就是將其列為行。</p>
<pre class="code">ghci&gt; transpose [[1,2,3],[4,5,6],[7,8,9]]  
[[1,4,7],[2,5,8],[3,6,9]]  
ghci&gt; transpose ["hey","there","guys"]  
["htg","ehu","yey","rs","e"]</pre>
<p>假如有兩個多項式 <code>3x&lt;sup&gt;2&lt;/sup&gt; + 5x + 9</code>，<code>10x&lt;sup&gt;3&lt;/sup&gt; + 9</code> 和 <code>8x&lt;sup&gt;3&lt;/sup&gt; + 5x&lt;sup&gt;2&lt;/sup&gt; + x - 1</code>，將其相加，我們可以列三個 List: <code>[0,3,5,9]</code>，<code>[10,0,0,9]</code> 和 <code>[8,5,1,-1]</code> 來表示。再用如下的方法取得結果.</p>
<pre class="code">ghci&gt; map sum $ transpose [[0,3,5,9],[10,0,0,9],[8,5,1,-1]]  
[18,8,6,17]</pre><img src="../img/legolists.png" alt="" style="float:left" class="floatleft" />
<p>使用 <code>transpose</code> 處理這三個 List 之後，三次冪就到了第一行，二次冪到了第二行，以此類推。在用 <code>sum</code> 函數將其映射，即可得到正確的結果。</p>

<p><strong>foldl'</strong> 和 <strong>foldl1'</strong> 是它們各自惰性實現的嚴格版本。在用 <code>fold</code> 處理較大的 List 時，經常會遇到堆棧溢出的問題。而這罪魁禍首就是 <code>fold</code> 的惰性: 在執行 <code>fold</code> 時，累加器的值並不會被立即更新，而是做一個"在必要時會取得所需的結果"的承諾。每過一遍累加器，這一行為就重複一次。而所有的這堆"承諾"最終就會塞滿你的堆棧。嚴格的 <code>fold</code> 就不會有這一問題，它們不會作"承諾"，而是直接計算中間值的結果並繼續執行下去。如果用惰性 <code>fold</code> 時經常遇到溢出錯誤，就應換用它們的嚴格版。</p>

<p><strong>concat</strong> 把一組 List 連接為一個 List。</p>
<pre class="code">ghci&gt; concat ["foo","bar","car"]  
"foobarcar"  
ghci&gt; concat [[3,4,5],[2,3,4],[2,1,1]]  
[3,4,5,2,3,4,2,1,1]</pre>
<p>它相當於移除一級嵌套。若要徹底地連接其中的元素，你得 <code>concat</code> 它兩次才行.</p>

<p><strong>concatMap</strong> 函數與 <code>map</code> 一個 List 之後再 <code>concat</code> 它等價.</p>
<pre class="code">ghci&gt; concatMap (replicate 4) [1..3]  
[1,1,1,1,2,2,2,2,3,3,3,3]</pre>
<p><strong>and</strong> 取一組布林值 List 作參數。只有其中的值全為 <code>True</code> 的情況下才會返回 <code>True</code>。</p>
<pre class="code">ghci&gt; and $ map (&gt;4) [5,6,7,8]  
True  
ghci&gt; and $ map (==4) [4,4,4,3,4]  
False</pre>
<p><strong>or</strong> 與 <code>and</code> 相似，一組布林值 List 中若存在一個 <code>True</code> 它就返回 <code>True</code>.</p>
<pre class="code">ghci&gt; or $ map (==4) [2,3,4,5,6,1]  
True  
ghci&gt; or $ map (&gt;4) [1,2,3]  
False</pre>
<p><strong>any</strong> 和 <strong>all</strong> 取一個限制條件和一組布林值 List 作參數，檢查是否該 List 的某個元素或每個元素都符合該條件。通常較 <code>map</code> 一個 List 到 <code>and</code> 或 <code>or</code> 而言，使用 <code>any</code> 或 <code>all</code> 會更多些。</p>
<pre class="code">ghci&gt; any (==4) [2,3,5,6,1,4]  
True  
ghci&gt; all (&gt;4) [6,9,10]  
True  
ghci&gt; all (`elem` ['A'..'Z']) "HEYGUYSwhatsup"  
False  
ghci&gt; any (`elem` ['A'..'Z']) "HEYGUYSwhatsup"  
True</pre>
<p><strong>iterate</strong> 取一個函數和一個值作參數。它會用該值去呼叫該函數並用所得的結果再次呼叫該函數，產生一個無限的 List.</p>
<pre class="code">ghci&gt; take 10 $ iterate (*2) 1  
[1,2,4,8,16,32,64,128,256,512]  
ghci&gt; take 3 $ iterate (++ "haha") "haha"  
["haha","hahahaha","hahahahahaha"]</pre>
<p><strong>splitAt</strong> 取一個 List 和數值作參數，將該 List 在特定的位置斷開。返回一個包含兩個 List 的二元組.</p>
<pre class="code">ghci&gt; splitAt 3 "heyman"  
("hey","man")  
ghci&gt; splitAt 100 "heyman"  
("heyman","")  
ghci&gt; splitAt (-3) "heyman"  
("","heyman")  
ghci&gt; let (a,b) = splitAt 3 "foobar" in b ++ a  
"barfoo"</pre>
<p><strong>takeWhile</strong> 這一函數十分的實用。它從一個 List 中取元素，一旦遇到不符合條件的某元素就停止.</p>
<pre class="code">ghci&gt; takeWhile (&gt;3) [6,5,4,3,2,1,2,3,4,5,4,3,2,1]  
[6,5,4]  
ghci&gt; takeWhile (/=' ') "This is a sentence"  
"This"</pre>
<p>如果要求所有三次方小於 1000 的數的和，用 <code>filter</code> 來過濾 <code>map (^3) [1..]</code> 所得結果中所有小於 1000 的數是不行的。因為對無限 List 執行的 <code>filter</code> 永遠都不會停止。你已經知道了這個 List 是單增的，但 Haskell 不知道。所以應該這樣：</p>
<pre class="code">ghci&gt; sum $ takeWhile (&lt;10000) $ map (^3) [1..]  
53361</pre>
<p>用 <code>(^3)</code> 處理一個無限 List，而一旦出現了大於 10000 的元素這個 List 就被切斷了，sum 到一起也就輕而易舉.</p>

<p><strong>dropWhile</strong> 與此相似，不過它是扔掉符合條件的元素。一旦限制條件返回 <code>False</code>，它就返回 List 的餘下部分。方便實用!</p>
<pre class="code">ghci&gt; dropWhile (/=' ') "This is a sentence"  
" is a sentence"  
ghci&gt; dropWhile (&lt;3) [1,2,2,2,3,4,5,4,3,2,1]  
[3,4,5,4,3,2,1]</pre>
<p>給一 <code>Tuple</code> 組成的 List，這 Tuple 的首项表示股票價格，第二三四項分別表示年,月,日。我們想知道它是在哪天首次突破 $1000 的!</p>
<pre class="code">ghci&gt; let stock = [(994.4,2008,9,1),(995.2,2008,9,2),(999.2,2008,9,3),(1001.4,2008,9,4),(998.3,2008,9,5)]  
ghci&gt; head (dropWhile (\(val,y,m,d) -&gt; val &lt; 1000) stock)  
(1001.4,2008,9,4)</pre>
<p><strong>span</strong> 與 <code>takeWhile</code> 有點像，只是它返回兩個 List。第一個 List 與同參數呼叫 <code>takeWhile</code> 所得的結果相同，第二個 List 就是原 List 中餘下的部分。</p>
<pre class="code">ghci&gt; let (fw，rest) = span (/=' ') "This is a sentence" in "First word:" ++ fw ++ "，the rest:" ++ rest  
"First word: This，the rest: is a sentence"</pre>
<p><strong>span</strong> 是在條件首次為 <code>False</code> 時斷開 List，而 <code>break</code> 則是在條件首次為 <code>True</code> 時斷開 <code>List</code>。<code>break p</code> 與 <code>span (not . p)</code> 是等價的.</p>
<pre class="code">ghci&gt; break (==4) [1,2,3,4,5,6,7]  
([1,2,3],[4,5,6,7])  
ghci&gt; span (/=4) [1,2,3,4,5,6,7]  
([1,2,3],[4,5,6,7])</pre>
<p><strong>break</strong> 返回的第二個 List 就會以第一個符合條件的元素開頭。</p>

<p><strong>sort</strong> 可以排序一個 List，因為只有能夠作比較的元素才可以被排序，所以這一 List 的元素必須是 Ord 型別類的實例型別。</p>
<pre class="code">ghci&gt; sort [8,5,3,2,1,6,4,2]  
[1,2,2,3,4,5,6,8]  
ghci&gt; sort "This will be sorted soon"  
" Tbdeehiillnooorssstw"</pre>
<p><strong>group</strong> 取一個 List 作參數，並將其中相鄰並相等的元素各自歸類，組成一個個子 List.</p>
<pre class="code">ghci&gt; group [1,1,1,1,2,2,2,2,3,3,2,2,2,5,6,7]  
[[1,1,1,1],[2,2,2,2],[3,3],[2,2,2],[5],[6],[7]]</pre>
<p>若在 <code>group</code> 一個 List 之前給它排序就可以得到每個元素在該 List 中的出現次數。</p>
<pre class="code">ghci&gt; map (\l@(x:xs) -&gt; (x,length l)) . group . sort $ [1,1,1,1,2,2,2,2,3,3,2,2,2,5,6,7]  
[(1,4),(2,7),(3,2),(5,1),(6,1),(7,1)]</pre>
<p><strong>inits</strong> 和 <strong>tails</strong> 與 <code>init</code> 和 <code>tail</code> 相似，只是它們會遞歸地呼叫自身直到什麼都不剩，看:</p>
<pre class="code">ghci&gt; inits "w00t"  
["","w","w0","w00","w00t"]  
ghci&gt; tails "w00t"  
["w00t","00t","0t","t",""]  
ghci&gt; let w = "w00t" in zip (inits w) (tails w)  
[("","w00t"),("w","00t"),("w0","0t"),("w00","t"),("w00t","")]</pre>
<p>我們用 <code>fold</code> 實現一個搜索子 List 的函數:</p>
<pre class="code">search :: (Eq a) =&gt; [a] -&gt; [a] -&gt; Bool  
search needle haystack =  
  let nlen = length needle  
  in foldl (\acc x -&gt; if take nlen x == needle then True else acc) False (tails haystack)</pre>
<p>首先，對搜索的 List 呼叫 <code>tails</code>，然後遍歷每個 List 來檢查它是不是我們想要的.</p>

<p>由此我們便實現了一個類似 <strong>isInfixOf</strong> 的函數，<strong>isInfixOf</strong> 從一個 List 中搜索一個子 List，若該 List 包含子 List，則返回 <code>True</code>.</p>
<pre class="code">ghci&gt; "cat" `isInfixOf` "im a cat burglar"  
True  
ghci&gt; "Cat" `isInfixOf` "im a cat burglar"  
False  
ghci&gt; "cats" `isInfixOf` "im a cat burglar"  
False</pre>
<p><strong>isPrefixOf</strong> 與 <strong>isSuffixOf</strong> 分別檢查一個 List 是否以某子 List 開頭或者結尾.</p>
<pre class="code">ghci&gt; "hey" `isPrefixOf` "hey there!"  
True  
ghci&gt; "hey" `isPrefixOf` "oh hey there!"  
False  
ghci&gt; "there!" `isSuffixOf` "oh hey there!"  
True  
ghci&gt; "there!" `isSuffixOf` "oh hey there"  
False</pre>
<p><strong>elem</strong> 與 <strong>notElem</strong> 檢查一個 List 是否包含某元素.</p>

<p><strong>partition</strong> 取一個限制條件和 List 作參數，返回兩個 List，第一個 List 中包含所有符合條件的元素，而第二個 List 中包含餘下的.</p>
<pre class="code">ghci&gt; partition (`elem` ['A'..'Z']) "BOBsidneyMORGANeddy"  
("BOBMORGAN","sidneyeddy")  
ghci&gt; partition (&gt;3) [1,3,5,6,3,2,1,0,3,7]  
([5,6,7],[1,3,3,2,1,0,3])</pre>
<p>瞭解這個與 <code>span</code> 和 <code>break</code> 的差異是很重要的.</p>
<pre class="code">ghci&gt; span (`elem` ['A'..'Z']) "BOBsidneyMORGANeddy"  
("BOB","sidneyMORGANeddy")</pre>
<p><code>span</code> 和 <code>break</code> 會在遇到第一個符合或不符合條件的元素處斷開，而 <code>partition</code> 則會遍歷整個 List。</p>

<p><strong>find</strong> 取一個 List 和限制條件作參數，並返回首個符合該條件的元素，而這個元素是個 <code>Maybe</code> 值。在下章，我們將深入地探討相關的算法和資料結構，但在這裡你只需瞭解 <code>Maybe</code> 值是 <code>Just something</code> 或 <code>Nothing</code> 就夠了。與一個 List 可以為空也可以包含多個元素相似，一個 <code>Maybe</code> 可以為空，也可以是單一元素。同樣與 List 類似，一個 Int 型的 List 可以寫作 <code>[Int]</code>，<code>Maybe</code>有個 Int 型可以寫作 <code>Maybe Int</code>。先試一下 <code>find</code> 函數再說.</p>
<pre class="code">ghci&gt; find (&gt;4) [1,2,3,4,5,6]  
Just 5  
ghci&gt; find (&gt;9) [1,2,3,4,5,6]  
Nothing  
ghci&gt; :t find  
find :: (a -&gt; Bool) -&gt; [a] -&gt; Maybe a</pre>
<p>注意一下 <code>find</code> 的型別，它的返回結果為 <code>Maybe a</code>，這與 <code>[a]</code> 的寫法有點像，只是 <code>Maybe</code> 型的值只能為空或者單一元素，而 List 可以為空,一個元素，也可以是多個元素.</p>

<p>想想前面那段找股票的程式碼，<code>head (dropWhile (\(val,y,m,d) -&gt; val &lt; 1000) stock)</code> 。但 <code>head</code> 並不安全! 如果我們的股票沒漲過 $1000 會怎樣? <code>dropWhile</code> 會返回一個空 List，而對空 List 取 <code>head</code> 就會引發一個錯誤。把它改成 <code>find (\(val,y,m,d) -&gt; val &gt; 1000) stock</code> 就安全多啦，若存在合適的結果就得到它, 像 <code>Just (1001.4,2008,9,4)</code>，若不存在合適的元素(即我們的股票沒有漲到過 $1000)，就會得到一個 <code>Nothing</code>.</p>

<p><strong>elemIndex</strong> 與 <code>elem</code> 相似，只是它返回的不是布林值，它只是'可能' (Maybe)返回我們找的元素的索引，若這一元素不存在，就返回 <code>Nothing</code>。</p>
<pre class="code">ghci&gt; :t elemIndex  
elemIndex :: (Eq a) =&gt; a -&gt; [a] -&gt; Maybe Int  
ghci&gt; 4 `elemIndex` [1,2,3,4,5,6]  
Just 3  
ghci&gt; 10 `elemIndex` [1,2,3,4,5,6]  
Nothing</pre>
<p><strong>elemIndices</strong> 與 <code>elemIndex</code> 相似，只不過它返回的是 List，就不需要 <code>Maybe</code> 了。因為不存在用空 List 就可以表示，這就與 <code>Nothing</code> 相似了.</p>
<pre class="code">ghci&gt; ' ' `elemIndices` "Where are the spaces?"  
[5,9,13]</pre>
<p><strong>findIndex</strong> 與 <code>find</code> 相似，但它返回的是可能存在的首個符合該條件元素的索引。<strong>findIndices</strong> 會返回所有符合條件的索引.</p>
<pre class="code">ghci&gt; findIndex (==4) [5,3,2,1,6,4]  
Just 5  
ghci&gt; findIndex (==7) [5,3,2,1,6,4]  
Nothing  
ghci&gt; findIndices (`elem` ['A'..'Z']) "Where Are The Caps?"  
[0,6,10,14]</pre>
<p>在前面，我們講過了 <code>zip</code> 和 <code>zipWith</code>，它們只能將兩個 List 組到一個二元組數或二參函數中，但若要組三個 List 該怎麼辦? 好說~ 有 <code>zip3</code>,<code>zip4</code>...,和 <code>zipWith3</code>, <code>zipWith4</code>...直到 7。這看起來像是個 hack，但工作良好。連着組 8 個 List 的情況很少遇到。還有個聰明辦法可以組起無限多個 List，但限於我們目前的水平，就先不談了.</p>
<pre class="code">ghci&gt; zipWith3 (\x y z -&gt; x + y + z) [1,2,3] [4,5,2,2] [2,2,3]  
[7,9,8]  
ghci&gt; zip4 [2,3,3] [2,2,2] [5,5,3] [2,2,2]  
[(2,2,5,2),(3,2,5,2),(3,2,3,2)]</pre>
<p>與普通的 <code>zip</code> 操作相似，以返回的 List 中長度最短的那個為準.</p>

<p>在處理來自檔案或其它地方的輸入時，<strong>lines</strong> 會非常有用。它取一個字串作參數。並返回由其中的每一行組成的 List.</p>
<pre class="code">ghci&gt; lines "first line\nsecond line\nthird line"  
["first line","second line","third line"]</pre>
<p><code>'\n'</code> 表示unix下的換行符，在 Haskell 的字元中，反斜杠表示特殊字元.</p>

<p><strong>unlines</strong> 是 <code>lines</code> 的反函數，它取一組字串的 List，並將其通過 <code>'\n'</code>合併到一塊.</p>
<pre class="code">ghci&gt; unlines ["first line"，"second line"，"third line"]  
"first line\nsecond line\nthird line\n"</pre>
<p><strong>words</strong> 和 <strong>unwords</strong> 可以把一個字串分為一組單詞或執行相反的操作，很有用.</p>
<pre class="code">ghci&gt; words "hey these are the words in this sentence"  
["hey","these","are","the","words","in","this","sentence"]  
ghci&gt; words "hey these are the words in this\nsentence"  
["hey","these","are","the","words","in","this","sentence"]  
ghci&gt; unwords ["hey","there","mate"]  
"hey there mate"</pre>
<p>我們前面講到了 <strong>nub</strong>，它可以將一個 List 中的重複元素全部篩掉，使該 List 的每個元素都如雪花般獨一無二，'nub' 的含義就是'一小塊'或'一部分'，用在這裡覺得很古怪。我覺得，在函數的命名上應該用更確切的詞語，而避免使用老掉牙的過時詞彙.</p>
<pre class="code">ghci&gt; nub [1,2,3,4,3,2,1,2,3,4,3,2,1]  
[1,2,3,4]  
ghci&gt; nub "Lots of words and stuff"  
"Lots fwrdanu"</pre>
<p><strong>delete</strong> 取一個元素和 List 作參數，會刪掉該 List 中首次出現的這一元素.</p>
<pre class="code">ghci&gt; delete 'h' "hey there ghang!"  
"ey there ghang!"  
ghci&gt; delete 'h' . delete 'h' $ "hey there ghang!"  
"ey tere ghang!"  
ghci&gt; delete 'h' . delete 'h' . delete 'h' $ "hey there ghang!"  
"ey tere gang!"</pre>
<p><strong>\\</strong> 表示 List 的差集操作，這與集合的差集很相似，它會除掉左邊 List 中所有存在於右邊 List 中的元素.</p>
<pre class="code">ghci&gt; [1..10] \\ [2,5,9]  
[1,3,4,6,7,8,10]  
ghci&gt; "Im a big baby" \\ "big"  
"Im a  baby"</pre>
<p><strong>union</strong> 與集合的並集也是很相似，它返回兩個 List 的並集，即遍歷第二個 List 若存在某元素不屬於第一個 List，則追加到第一個 List。看，第二個 List 中的重複元素就都沒了!</p>
<pre class="code">ghci&gt; "hey man" `union` "man what's up"  
"hey manwt'sup"  
ghci&gt; [1..7] `union` [5..10]  
[1,2,3,4,5,6,7,8,9,10]</pre>
<p><strong>intersection</strong> 相當於集合的交集。它返回兩個 List 的相同部分.</p>
<pre class="code">ghci&gt; [1..7] `intersect` [5..10]  
[5,6,7]</pre>
<p><strong>insert</strong> 可以將一個元素插入一個可排序的 List，並將其置於首個大於等於它的元素之前，如果使用 <code>insert</code> 來給一個排過序的 List 插入元素，返回的結果依然是排序的.</p>
<pre class="code">ghci&gt; insert 4 [1,2,3,5,6,7]  
[1,2,3,4,5,6,7]  
ghci&gt; insert 'g' $ ['a'..'f'] ++ ['h'..'z']  
"abcdefghijklmnopqrstuvwxyz"  
ghci&gt; insert 3 [1,2,4,3,2,1]  
[1,2,3,4,3,2,1]</pre>
<p><code>length</code>，<code>take</code>，<code>drop</code>，<code>splitAt</code>，<code>!!</code> 和 <code>replicate</code> 之類的函數有個共同點。那就是它們的參數中都有個 Int 值（或者返回Int值），我覺得使用 Intergal 或 Num 型別類會更好，但出於歷史原因，修改這些會破壞掉許多既有的程式碼。在 <code>Data.List</code> 中包含了更通用的替代版，如: <code>genericLength，genericTake，genericDrop，genericSplitAt，genericIndex</code> 和 <code>genericReplicate</code>。<code>length</code> 的型別聲明為 <code> length :: [a] -&gt; Int</code>，而我們若要像這樣求它的平均值，<code>let xs = [1..6] in sum xs / length xs</code> ，就會得到一個型別錯誤，因為 <code>/</code> 運算符不能對 Int 型使用! 而 <code>genericLength</code> 的型別聲明則為 <code>genericLength :: (Num a) =&gt; [b] -&gt; a</code>，Num 既可以是整數又可以是浮點數，<code>let xs = [1..6] in sum xs / genericLength xs</code> 這樣再求平均數就不會有問題了.</p>

<p><code>nub</code>, <code>delete</code>, <code>union</code>, <code>intsect</code> 和 <code>group</code> 函數也有各自的通用替代版 <code>nubBy</code>，<code>deleteBy</code>，<code>unionBy</code>，<code>intersectBy</code> 和 <code>groupBy</code>，它們的區別就是前一組函數使用 <code>(==)</code> 來測試是否相等，而帶 <code>By</code> 的那組則取一個函數作參數來判定相等性，<code>group</code> 就與 <code>groupBy (==)</code> 等價.</p>

<p>假如有個記錄某函數在每秒的值的 List，而我們要按照它小於零或者大於零的交界處將其分為一組子 List。如果用 <code>group</code>，它只能將相鄰並相等的元素組到一起，而在這裡我們的標準是它們是否互爲相反數。<code>groupBy</code> 登場! 它取一個含兩個參數的函數作為參數來判定相等性.</p>
<pre class="code">ghci&gt; let values = [-4.3，-2.4，-1.2，0.4，2.3，5.9，10.5，29.1，5.3，-2.4，-14.5，2.9，2.3]  
ghci&gt; groupBy (\x y -&gt; (x &gt; 0) == (y &gt; 0)) values  
[[-4.3,-2.4,-1.2],[0.4,2.3,5.9,10.5,29.1,5.3],[-2.4,-14.5],[2.9,2.3]]</pre>
<p>這樣一來我們就可以很清楚地看出哪部分是正數，哪部分是負數，這個判斷相等性的函數會在兩個元素同時大於零或同時小於零時返回 <code>True</code>。也可以寫作 <code>\x y -&gt; (x &gt; 0) &amp;&amp; (y &gt; 0) || (x &lt;= 0) &amp;&amp; (y &lt;= 0)</code>。但我覺得第一個寫法的可讀性更高。<code>Data.Function</code> 中還有個 <code>on</code> 函數可以讓它的表達更清晰，其定義如下:</p>
<pre class="code">on :: (b -&gt; b -&gt; c) -&gt; (a -&gt; b) -&gt; a -&gt; a -&gt; c  
f `on` g = \x y -&gt; f (g x) (g y)</pre>
<p>執行 <code>(==) `on` (&gt; 0)</code> 得到的函數就與 <code>\x y -&gt; (x &gt; 0) == (y &gt; 0)</code> 基本等價。<code>on</code> 與帶 <code>By</code> 的函數在一起會非常好用，你可以這樣寫:</p>
<pre class="code">ghci&gt; groupBy ((==) `on` (&gt; 0)) values  
[[-4.3,-2.4,-1.2],[0.4,2.3,5.9,10.5,29.1,5.3],[-2.4,-14.5],[2.9,2.3]]</pre>
<p>可讀性很高! 你可以大聲念出來: 按照元素是否大於零，給它分類！</p>

<p>同樣，<code>sort</code>，<code>insert</code>，<code>maximum</code> 和 <code>min</code> 都有各自的通用版本。如 <code>groupBy</code> 類似，<strong>sortBy</strong>，<strong>insertBy</strong>，<strong>maximumBy</strong> 和 <strong>minimumBy</strong> 都取一個函數來比較兩個元素的大小。像 <code>sortBy</code> 的型別聲明為:  <code>sortBy :: (a -&gt; a -&gt; Ordering) -&gt; [a] -&gt; [a]</code>。前面提過，<code>Ordering</code> 型別可以有三個值,<code>LT</code>，<code>EQ</code> 和 <code>GT</code>。<code>compare</code> 取兩個 <code>Ord</code> 型別類的元素作參數，所以 <code>sort</code> 與 <code>sortBy compare</code> 等價.</p>

<p>List 是可以比較大小的，且比較的依據就是其中元素的大小。如果按照其子 List 的長度為標準當如何? 很好，你可能已經猜到了，<code>sortBy</code> 函數.</p>
<pre class="code">ghci&gt; let xs = [[5,4,5,4,4],[1,2,3],[3,5,4,3],[],[2],[2,2]]  
ghci&gt; sortBy (compare `on` length) xs  
[[],[2],[2,2],[1,2,3],[3,5,4,3],[5,4,5,4,4]]</pre>
<p>太絶了! <code>compare `on` length</code>，乖乖，這簡直就是英文! 如果你搞不清楚 <code>on</code> 在這裡的原理，就可以認為它與 <code>\x y -&gt; length x `compare` length y</code> 等價。通常，與帶 <code>By</code> 的函數打交道時，若要判斷相等性，則 <code>(==) `on` something</code>。若要判定大小，則 <code>compare `on` something</code>.</p>
<a name="Data.Char"></a><h2>Data.Char</h2>


<p>如其名，<code>Data.Char</code> 模組包含了一組用於處理字元的函數。由於字串的本質就是一組字元的 List，所以往往會在 <code>filter</code> 或是 <code>map</code> 字串時用到它.</p>

<p><code>Data.Char</code>模組中含有一系列用於判定字元範圍的函數，如下:</p>
<img src="../img/legochar.png" alt="" style="float:right" class="floatright" />
<p><strong>isControl</strong> 判斷一個字元是否是控制字元。</p>

<p><strong>isSpace</strong> 判斷一個字元是否是空格字元，包括空格，tab，換行符等.</p>

<p><strong>isLower</strong> 判斷一個字元是否為小寫.</p>

<p><strong>isUper</strong> 判斷一個字元是否為大寫。</p>

<p><strong>isAlpha</strong> 判斷一個字元是否為字母.</p>

<p><strong>isAlphaNum</strong> 判斷一個字元是否為字母或數字.</p>

<p><strong>isPrint</strong> 判斷一個字元是否是可打印的.</p>

<p><strong>isDigit</strong> 判斷一個字元是否為數字.</p>

<p><strong>isOctDigit</strong> 判斷一個字元是否為八進制數字.</p>

<p><strong>isHexDigit</strong> 判斷一個字元是否為十六進制數字.</p>

<p><strong>isLetter</strong> 判斷一個字元是否為字母.</p>

<p><strong>isMark</strong> 判斷是否為 unicode 注音字元，你如果是法國人就會經常用到的.</p>

<p><strong>isNumber</strong> 判斷一個字元是否為數字.</p>

<p><strong>isPunctuation</strong> 判斷一個字元是否為標點符號.</p>

<p><strong>isSymbol</strong>判斷一個字元是否為貨幣符號.</p>

<p><strong>isSeperater</strong> 判斷一個字元是否為 unicode 空格或分隔符.</p>

<p><strong>isAscii</strong> 判斷一個字元是否在 unicode 字母表的前 128 位。</p>

<p><strong>isLatin1</strong> 判斷一個字元是否在 unicode 字母表的前 256 位.</p>

<p><strong>isAsciiUpper</strong> 判斷一個字元是否為大寫的 ascii 字元.</p>

<p><strong>isAsciiLower</strong> 判斷一個字元是否為小寫的 ascii 字元.</p>

<p>以上所有判斷函數的型別聲明皆為 <code>Char -&gt; Bool</code>，用到它們的絶大多數情況都無非就是過濾字串或類似操作。假設我們在寫個程序，它需要一個由字元和數字組成的用戶名。要實現對用戶名的檢驗，我們可以結合使用 <code>Data.List</code> 模組的 <code>all</code> 函數與 <code>Data.Char</code> 的判斷函數.</p>
<pre class="code">ghci&gt; all isAlphaNum "bobby283"  
True  
ghci&gt; all isAlphaNum "eddy the fish!"  
False</pre>
<p>Kewl~ 免得你忘記，<code>all</code> 函數取一個判斷函數和一個 List 做參數，若該 List 的所有元素都符合條件，就返回 <code>True</code>.</p>

<p>也可以使用 <code>isSpace</code> 來實現 <code>Data.List</code> 的 <code>words</code> 函數.</p>
<pre class="code">ghci&gt; words "hey guys its me"  
["hey","guys","its","me"]  
ghci&gt; groupBy ((==) `on` isSpace) "hey guys its me"  
["hey"," ","guys"," ","its"," ","me"]  
ghci&gt;</pre>
<p>Hmm，不錯，有點 <code>words</code> 的樣子了。只是還有空格在裡面，恩，該怎麼辦? 我知道，用 <code>filter</code> 濾掉它們!</p>
<pre class="code">ghci&gt; filter (not . any isSpace) . groupBy ((==) `on` isSpace) $ "hey guys its me"  
["hey","guys","its","me"]</pre>
<p>啊哈.</p>

<p><code>Data.Char</code> 中也含有與 <code>Ordering</code> 相似的型別。<code>Ordering</code> 可以有兩個值，<code>LT</code>，<code>GT</code> 和 <code>EQ</code>。這就是個枚舉，它表示了兩個元素作比較可能的結果. <code>GeneralCategory</code> 型別也是個枚舉，它表示了一個字元可能所在的分類。而得到一個字元所在分類的主要方法就是使用  <code>generalCategory</code>  函數.它的型別為: <code>generalCategory :: Char -&gt; GeneralCategory</code>。那 31 個分類就不在此一一列出了，試下這個函數先:</p>
<pre class="code">ghci&gt; generalCategory ' '  
Space  
ghci&gt; generalCategory 'A'  
UppercaseLetter  
ghci&gt; generalCategory 'a'  
LowercaseLetter  
ghci&gt; generalCategory '.'  
OtherPunctuation  
ghci&gt; generalCategory '9'  
DecimalNumber  
ghci&gt; map generalCategory " \t\nA9?|"  
[Space,Control,Control,UppercaseLetter,DecimalNumber,OtherPunctuation,MathSymbol]</pre>
<p>由於 <code>GeneralCategory</code> 型別是 <code>Eq</code> 型別類的一部分，使用類似 <code>generalCategory c == Space</code> 的程式碼也是可以的.</p>

<p><strong>toUpper</strong> 將一個字元轉為大寫字母，若該字元不是小寫字母，就按原值返回.</p>

<p><strong>toLower</strong> 將一個字元轉為小寫字母，若該字元不是大寫字母，就按原值返回.</p>

<p><strong>toTitle</strong> 將一個字元轉為 title-case，對大多數字元而言，title-case 就是大寫.</p>

<p><strong>digitToInt</strong> 將一個字元轉為 Int 值，而這一字元必須得在 <code>'1'..'9','a'..'f'</code>或<code>'A'..'F'</code> 的範圍之內.</p>
<pre class="code">ghci&gt; map digitToInt "34538"  
[3,4,5,3,8]  
ghci&gt; map digitToInt "FF85AB"  
[15,15,8,5,10,11]</pre>
<p><code>intToDigit</code> 是 <code>digitToInt</code> 的反函數。它取一個 <code>0</code> 到 <code>15</code> 的 <code>Int</code> 值作參數，並返回一個小寫的字元.</p>
<pre class="code">ghci&gt; intToDigit 15  
'f'  
ghci&gt; intToDigit 5  
'5'</pre>
<p><strong>ord</strong> 與 <strong>char</strong> 函數可以將字元與其對應的數字相互轉換.</p>
<pre class="code">ghci&gt; ord 'a'  
97  
ghci&gt; chr 97  
'a'  
ghci&gt; map ord "abcdefgh"  
[97,98,99,100,101,102,103,104]</pre>
<p>兩個字元的 <code>ord</code> 值之差就是它們在 unicode 字元表上的距離.</p>

<p><b>Caesar ciphar</b> 是加密的基礎算法，它將消息中的每個字元都按照特定的字母表進行替換。它的實現非常簡單，我們這裡就先不管字母表了.</p>
<pre class="code">encode :: Int -&gt; String -&gt; String  
encode shift msg = 
  let ords = map ord msg  
      shifted = map (+ shift) ords  
  in map chr shifted</pre>
<p>先將一個字串轉為一組數字，然後給它加上某數，再轉回去。如果你是標準的組合牛仔，大可將函數寫為: <code>map (chr . (+ shift) . ord) msg</code>。試一下它的效果:</p>
<pre class="code">ghci&gt; encode 3 "Heeeeey"  
"Khhhhh|"  
ghci&gt; encode 4 "Heeeeey"  
"Liiiii}"  
ghci&gt; encode 1 "abcd"  
"bcde"  
ghci&gt; encode 5 "Marry Christmas! Ho ho ho!"  
"Rfww~%Hmwnxyrfx&amp;%Mt%mt%mt&amp;"</pre>
<p>不錯。再簡單地將它轉成一組數字，減去某數後再轉回來就是解密了.</p>
<pre class="code">decode :: Int -&gt; String -&gt; String  
decode shift msg = encode (negate shift) msg</pre><pre class="code">ghci&gt; encode 3 "Im a little teapot"  
"Lp#d#olwwoh#whdsrw"  
ghci&gt; decode 3 "Lp#d#olwwoh#whdsrw"  
"Im a little teapot"  
ghci&gt; decode 5 . encode 5 $ "This is a sentence"  
"This is a sentence"</pre><a name="Data.Map"></a><h2>Data.Map</h2>


<p>關聯列表(也叫做字典)是按照鍵值對排列而沒有特定順序的一種 List。例如，我們用關聯列表儲存電話號碼，號碼就是值，人名就是鍵。我們並不關心它們的存儲順序，只要能按人名得到正確的號碼就好.在 Haskell 中表示關聯列表的最簡單方法就是弄一個二元組的 List，而這二元組就首項為鍵，後項為值。如下便是個表示電話號碼的關聯列表: </p>
<pre class="code">phoneBook = [("betty","555-2938") ,
             ("bonnie","452-2928") ,
             ("patsy","493-2928") ,
             ("lucille","205-2928") ,
             ("wendy","939-8282") ,
             ("penny","853-2492") ]</pre>
<p>不理這貌似古怪的縮進，它就是一組二元組的 List 而已。話說對關聯列表最常見的操作就是按鍵索值，我們就寫個函數來實現它。</p>
<pre class="code">findKey :: (Eq k) =&gt; k -&gt; [(k,v)] -&gt; v 
findKey key xs = snd . head . filter (\(k,v) -&gt; key == k) $ xs</pre><img src="../img/legomap.png" alt="" style="float:right" class="floatright" />
<p>簡潔漂亮。這個函數取一個鍵和 List 做參數，過濾這一 List 僅保留鍵匹配的項，並返迴首個鍵值對。但若該關聯列表中不存在這個鍵那會怎樣? 哼，那就會在試圖從空 List 中取 <code>head</code> 時引發一個運行時錯誤。無論如何也不能讓程序就這麼輕易地崩潰吧，所以就應該用 <code>Maybe</code> 型別。如果沒找到相應的鍵，就返回 <code>Nothing</code>。而找到了就返回 <code>Just something</code>。而這 <code>something</code> 就是鍵對應的值。</p>
<pre class="code">findKey :: (Eq k) =&gt; k -&gt; [(k,v)] -&gt; Maybe v 
findKey key [] = Nothing
findKey key ((k,v):xs) = 
     if key == k then 
         Just v 
     else 
         findKey key xs</pre>
<p>看這型別聲明，它取一個可判斷相等性的鍵和一個關聯列表做參數，可能 (Maybe) 得到一個值。聽起來不錯.這便是個標準的處理 List 的遞歸函數，邊界條件，分割 List，遞歸呼叫，都有了 -- 經典的 <code>fold</code> 模式。</p>

<p>看看用 <code>fold</code> 怎樣實現吧。</p>
<pre class="code">findKey :: (Eq k) =&gt; k -&gt; [(k,v)] -&gt; Maybe v 
findKey key = foldr (\(k,v) acc -&gt; if key == k then Just v else acc) Nothing</pre><blockquote>
<p><b>Note</b>: 通常，使用 <code>fold</code> 來替代類似的遞歸函數會更好些。用 <code>fold</code> 的程式碼讓人一目瞭然，而看明白遞歸則得多花點腦子。</p>
</blockquote>
<pre class="code">ghci&gt; findKey "penny" phoneBook 
Just "853-2492" 
ghci&gt; findKey "betty" phoneBook 
Just "555-2938" 
ghci&gt; findKey "wilma" phoneBook 
Nothing</pre>
<p>如魔咒般靈驗! 只要我們有這姑娘的號碼就 <code>Just</code> 可以得到，否則就是 <code>Nothing</code>. 方纔我們實現的函數便是 <code>Data.List</code> 模組的 <code>lookup</code>，如果要按鍵去尋找相應的值，它就必須得遍歷整個 List，直到找到為止。而 <code>Data.Map</code> 模組提供了更高效的方式(通過樹實現)，並提供了一組好用的函數。從現在開始，我們扔掉關聯列表，改用map.由於<code>Data.Map</code>中的一些函數與Prelude和<code>Data.List</code> 模組存在命名衝突，所以我們使用 <code>qualified import</code>。<code>import qualified Data.Map as Map</code> 在程式碼中加上這句，並 <code>load</code> 到 ghci 中.繼續前進，看看 <code>Data.Map</code> 是如何的一座寶庫! </p>

<p>如下便是其中函數的一瞥:</p>

<p><strong>fromList</strong> 取一個關聯列表，返回一個與之等價的 Map。</p>
<pre class="code">ghci&gt; Map.fromList [("betty","555-2938"),("bonnie","452-2928"),("lucille","205-2928")] 
fromList [("betty","555-2938"),("bonnie","452-2928"),("lucille","205-2928")] 
ghci&gt; Map.fromList [(1,2),(3,4),(3,2),(5,5)] 
fromList [(1,2),(3,2),(5,5)]</pre>
<p>若其中存在重複的鍵,就將其忽略。如下即 <code>fromList</code> 的型別聲明。</p>
<pre class="code">Map.fromList :: (Ord k) =&gt; [(k，v)] -&gt; Map.Map k v</pre>
<p>這表示它取一組鍵值對的 List，並返回一個將 <code>k</code> 映射為 <code>v</code> 的 <code>map</code>。注意一下，當使用普通的關聯列表時，只需要鍵的可判斷相等性就行了。而在這裡，它還必須得是可排序的。這在 <code>Data.Map</code> 模組中是強制的。因為它會按照某順序將其組織在一棵樹中.在處理鍵值對時，只要鍵的型別屬於 <code>Ord</code> 型別類，就應該儘量使用<code>Data.Map</code>.<code>empty</code> 返回一個空 <code>map</code>.</p>
<pre class="code">ghci&gt; Map.empty 
fromList []</pre>
<p><strong>insert</strong> 取一個鍵，一個值和一個 <code>map</code> 做參數，給這個 <code>map</code> 插入新的鍵值對，並返回一個新的 <code>map</code>。</p>
<pre class="code">ghci&gt; Map.empty 
fromList [] 
ghci&gt; Map.insert 3 100 Map.empty
fromList [(3,100)] 
ghci&gt; Map.insert 5 600 (Map.insert 4 200 ( Map.insert 3 100  Map.empty)) 
fromList [(3,100),(4,200),(5,600)]
ghci&gt; Map.insert 5 600 . Map.insert 4 200 . Map.insert 3 100 $ Map.empty 
fromList [(3,100),(4,200),(5,600)]</pre>
<p>通過 <code>empty</code>，<code>insert</code> 與 <code>fold</code>，我們可以編寫出自己的 <code>fromList</code>。</p>
<pre class="code">fromList' :: (Ord k) =&gt; [(k,v)] -&gt; Map.Map k v 
fromList' = foldr (\(k,v) acc -&gt; Map.insert k v acc) Map.empty</pre>
<p>簡潔明瞭的 <code>fold</code>！ 從一個空的 <code>map</code> 開始，然後從右摺疊，隨着遍歷不斷地往 <code>map</code> 中插入新的鍵值對.</p>

<p><strong>null</strong> 檢查一個 <code>map</code> 是否為空.</p>
<pre class="code">ghci&gt; Map.null Map.empty 
True 
ghci&gt; Map.null $ Map.fromList [(2,3),(5,5)] 
False</pre>
<p><strong>size</strong> 返回一個 <code>map</code> 的大小。</p>
<pre class="code">ghci&gt; Map.size Map.empty 
0 
ghci&gt; Map.size $ Map.fromList [(2,4),(3,3),(4,2),(5,4),(6,4)] 
5</pre>
<p><strong>singleton</strong> 取一個鍵值對做參數,並返回一個只含有一個映射的 <code>map</code>.</p>
<pre class="code">ghci&gt; Map.singleton 3 9 
fromList [(3,9)] 
ghci&gt; Map.insert 5 9 $ Map.singleton 3 9 
fromList [(3,9),(5,9)]</pre>
<p><strong>lookup</strong> 與 <code>Data.List</code> 的 <code>lookup</code> 很像,只是它的作用對象是 <code>map</code>，如果它找到鍵對應的值。就返回 <code>Just something</code>，否則返回 <code>Nothing</code>。</p>

<p><strong>member</strong> 是個判斷函數，它取一個鍵與 <code>map</code> 做參數，並返回該鍵是否存在於該 <code>map</code>。</p>
<pre class="code">ghci&gt; Map.member 3 $ Map.fromList [(3,6),(4,3),(6,9)] 
True 
ghci&gt; Map.member 3 $ Map.fromList [(2,5),(4,5)] 
False</pre>
<p><strong>map</strong> 與 <strong>filter</strong> 與其對應的 <code>List</code> 版本很相似: </p>
<pre class="code">ghci&gt; Map.map (*100) $ Map.fromList [(1,1),(2,4),(3,9)] 
fromList [(1,100),(2,400),(3,900)] 
ghci&gt; Map.filter isUpper $ Map.fromList [(1,'a'),(2,'A'),(3,'b'),(4,'B')] 
fromList [(2,'A'),(4,'B')]</pre>
<p><code>toList</code> 是 <code>fromList</code> 的反函數。</p>
<pre class="code">ghci&gt; Map.toList . Map.insert 9 2 $ Map.singleton 4 3 
[(4,3),(9,2)]</pre>
<p><strong>keys</strong> 與 <strong>elems</strong> 各自返回一組由鍵或值組成的 List，<code>keys</code> 與 <code>map fst . Map.toList</code> 等價，<code>elems</code> 與 <code>map snd . Map.toList</code>等價. <code>fromListWith</code> 是個很酷的小函數，它與 <code>fromList</code> 很像，只是它不會直接忽略掉重複鍵，而是交給一個函數來處理它們。假設一個姑娘可以有多個號碼，而我們有個像這樣的關聯列表: </p>
<pre class="code">phoneBook =   
    [("betty","555-2938")  
    ,("betty","342-2492")  
    ,("bonnie","452-2928")  
    ,("patsy","493-2928")  
    ,("patsy","943-2929")  
    ,("patsy","827-9162")  
    ,("lucille","205-2928")  
    ,("wendy","939-8282")  
    ,("penny","853-2492")  
    ,("penny","555-2111")  
    ]</pre>
<p>如果用 <code>fromList</code> 來生成 <code>map</code>，我們會丟掉許多號碼! 如下才是正確的做法: </p>
<pre class="code">phoneBookToMap :: (Ord k) =&gt; [(k, String)] -&gt; Map.Map k String  
phoneBookToMap xs = Map.fromListWith (\number1 number2 -&gt; number1 ++ ", " ++ number2) xs</pre><pre class="code">ghci&gt; Map.lookup "patsy" $ phoneBookToMap phoneBook 
"827-9162, 943-2929, 493-2928" 
ghci&gt; Map.lookup "wendy" $ phoneBookToMap phoneBook
"939-8282" 
ghci&gt; Map.lookup "betty" $ phoneBookToMap phoneBook 
"342-2492，555-2938"</pre>
<p>一旦出現重複鍵，這個函數會將不同的值組在一起，同樣，也可以預設地將每個值放到一個單元素的 List 中，再用 <code>++</code> 將他們都連接在一起。</p>
<pre class="code">phoneBookToMap :: (Ord k) =&gt; [(k，a)] -&gt; Map.Map k [a] 
phoneBookToMap xs = Map.fromListWith (++) $ map (\(k,v) -&gt; (k,[v])) xs 
ghci&gt; Map.lookup "patsy" $ phoneBookToMap phoneBook 
["827-9162","943-2929","493-2928"]</pre>
<p>很簡潔! 它還有別的玩法，例如在遇到重複元素時，單選最大的那個值.</p>
<pre class="code">ghci&gt; Map.fromListWith max [(2,3),(2,5),(2,100),(3,29),(3,22),(3,11),(4,22),(4,15)] 
fromList [(2,100),(3,29),(4,22)]</pre>
<p>或是將相同鍵的值都加在一起.</p>
<pre class="code">ghci&gt; Map.fromListWith (+) [(2,3),(2,5),(2,100),(3,29),(3,22),(3,11),(4,22),(4,15)] 
fromList [(2,108),(3,62),(4,37)]</pre>
<p><strong>insertWith</strong> 之於 <code>insert</code>，恰如 <code>fromListWith</code> 之於 <code>fromList</code>。它會將一個鍵值對插入一個 <code>map</code> 之中，而該 <code>map</code> 若已經包含這個鍵，就問問這個函數該怎麼辦。</p>
<pre class="code">ghci&gt; Map.insertWith (+) 3 100 $ Map.fromList [(3,4),(5,103),(6,339)] 
fromList [(3,104),(5,103),(6,339)]</pre>
<p><code>Data.Map</code> 裡面還有不少函數，<a href="http://www.haskell.org/ghc/docs/latest/html/libraries/containers/Data-Map.html">這個文檔</a>中的列表就很全了.</p>
<a name="Data.Set"></a><h2>Data.Set</h2>

<img src="../img/legosets.png" alt="" style="float:right" class="floatright" />
<p><code>Data.Set</code> 模組提供了對數學中集合的處理。集合既像 List 也像 <code>Map</code>: 它裡面的每個元素都是唯一的，且內部的數據由一棵樹來組織(這和 <code>Data.Map</code> 模組的 <code>map</code> 很像)，必須得是可排序的。同樣是插入,刪除,判斷從屬關係之類的操作，使用集合要比 List 快得多。對一個集合而言，最常見的操作莫過于並集，判斷從屬或是將集合轉為 List.</p>

<p>由於 <code>Data.Set</code> 模組與 <code>Prelude</code> 模組和 <code>Data.List</code> 模組中存在大量的命名衝突，所以我們使用 <code>qualified import</code></p>

<p>將 <code>import</code> 語句至于程式碼之中:</p>
<pre class="code">import qualified Data.Set as Set</pre>
<p>然後在 ghci 中裝載</p>

<p>假定我們有兩個字串，要找出同時存在於兩個字串的字元</p>
<pre class="code">text1 = "I just had an anime dream. Anime... Reality... Are they so different?"  
text2 = "The old man left his garbage can out and now his trash is all over my lawn!"</pre>
<p><strong>fromList</strong> 函數同你想的一樣，它取一個 List 作參數並將其轉為一個集合  </p>
<pre class="code">ghci&gt; let set1 = Set.fromList text1  
ghci&gt; let set2 = Set.fromList text2  
ghci&gt; set1  
fromList " .?AIRadefhijlmnorstuy"  
ghci&gt; set2  
fromList " !Tabcdefghilmnorstuvwy"</pre>
<p>如你所見，所有的元素都被排了序。而且每個元素都是唯一的。現在我們取它的交集看看它們共同包含的元素:</p>
<pre class="code">ghci&gt; Set.intersection set1 set2  
fromList " adefhilmnorstuy"</pre>
<p>使用 <code>difference</code> 函數可以得到存在於第一個集合但不在第二個集合的元素  </p>
<pre class="code">ghci&gt; Set.difference set1 set2  
fromList ".?AIRj"  
ghci&gt; Set.difference set2 set1  
fromList "!Tbcgvw"</pre>
<p>也可以使用 <code>union</code> 得到兩個集合的並集  </p>
<pre class="code">ghci&gt; Set.union set1 set2  
fromList " !.?AIRTabcdefghijlmnorstuvwy"</pre>
<p><code>null</code>，<code>size</code>，<code>member</code>，<code>empty</code>，<code>singleton</code>，<code>insert</code>，<code>delete</code> 這幾個函數就跟你想的差不多啦  </p>
<pre class="code">ghci&gt; Set.null Set.empty  
True  
ghci&gt; Set.null $ Set.fromList [3,4,5,5,4,3]  
False  
ghci&gt; Set.size $ Set.fromList [3,4,5,3,4,5]  
3  
ghci&gt; Set.singleton 9  
fromList [9]  
ghci&gt; Set.insert 4 $ Set.fromList [9,3,8,1]  
fromList [1,3,4,8,9]  
ghci&gt; Set.insert 8 $ Set.fromList [5..10]  
fromList [5,6,7,8,9,10]  
ghci&gt; Set.delete 4 $ Set.fromList [3,4,5,4,3,4,5]  
fromList [3,5]</pre>
<p>也可以判斷子集與真子集，如果集合 A 中的元素都屬於集合 B，那麼 A 就是 B 的子集, 如果 A 中的元素都屬於 B 且 B 的元素比 A 多，那 A 就是 B 的真子集  </p>
<pre class="code">ghci&gt; Set.fromList [2,3,4] `Set.isSubsetOf` Set.fromList [1,2,3,4,5]  
True  
ghci&gt; Set.fromList [1,2,3,4,5] `Set.isSubsetOf` Set.fromList [1,2,3,4,5]  
True  
ghci&gt; Set.fromList [1,2,3,4,5] `Set.isProperSubsetOf` Set.fromList [1,2,3,4,5]  
False  
ghci&gt; Set.fromList [2,3,4,8] `Set.isSubsetOf` Set.fromList [1,2,3,4,5]  
False</pre>
<p>對集合也可以執行 <code>map</code> 和 <code>filter</code>:</p>
<pre class="code">ghci&gt; Set.filter odd $ Set.fromList [3,4,5,6,7,2,3,4]  
fromList [3,5,7]  
ghci&gt; Set.map (+1) $ Set.fromList [3,4,5,6,7,2,3,4]  
fromList [3,4,5,6,7,8]</pre>
<p>集合有一常見用途，那就是先 <code>fromList</code> 刪掉重複元素後再 <code>toList</code> 轉回去。儘管 <code>Data.List</code> 模組的 <code>nub</code> 函數完全可以完成這一工作，但在對付大 List 時則會明顯的力不從心。使用集合則會快很多，<code>nub</code> 函數只需 List 中的元素屬於 <code>Eq</code> 型別類就行了，而若要使用集合，它必須得屬於 <code>Ord</code> 型別類  </p>
<pre class="code">ghci&gt; let setNub xs = Set.toList $ Set.fromList xs  
ghci&gt; setNub "HEY WHATS CRACKALACKIN"  
" ACEHIKLNRSTWY"  
ghci&gt; nub "HEY WHATS CRACKALACKIN"  
"HEY WATSCRKLIN"</pre>
<p>在處理較大的 List 時，<code>setNub</code> 要比 <code>nub</code> 快，但也可以從中看出，<code>nub</code> 保留了 List 中元素的原有順序，而 <code>setNub</code> 不。</p>
<a name="建立自己的模組"></a><h2>建立自己的模組</h2>


<p>我們已經見識過了幾個很酷的模組，但怎樣才能構造自己的模組呢? 几乎所有的編程語言都允許你將程式碼分成多個檔案，Haskell 也不例外。在編程時，將功能相近的函數和型別至于同一模組中會是個很好的習慣。這樣一來，你就可以輕鬆地一個 <code>import</code> 來重用其中的函數.</p>

<p>接下來我們將構造一個由計算機幾何圖形體積和面積組成的模組，先從新建一個 <code>Geometry.hs</code> 的檔案開始.</p>

<p>在模組的開頭定義模組的名稱，如果檔案名叫做 <code>Geometry.hs</code> 那它的名字就得是 <code>Geometry</code>。在聲明出它含有的函數名之後就可以編寫函數的實現啦，就這樣寫:</p>
<pre class="code">module Geometry  
( sphereVolume  
，sphereArea  
，cubeVolume  
，cubeArea  
，cuboidArea  
，cuboidVolume  
) where</pre>
<p>如你所見，我們提供了對球體,立方體和立方體的面積和體積的解法。繼續進發，定義函數體:</p>
<pre class="code">module Geometry  
( sphereVolume  
，sphereArea  
，cubeVolume  
，cubeArea  
，cuboidArea  
，cuboidVolume  
) where  

sphereVolume :: Float -&gt; Float  
sphereVolume radius = (4.0 / 3.0) * pi * (radius ^ 3)  

sphereArea :: Float -&gt; Float  
sphereArea radius = 4 * pi * (radius ^ 2)  

cubeVolume :: Float -&gt; Float  
cubeVolume side = cuboidVolume side side side  

cubeArea :: Float -&gt; Float  
cubeArea side = cuboidArea side side side  

cuboidVolume :: Float -&gt; Float -&gt; Float -&gt; Float  
cuboidVolume a b c = rectangleArea a b * c  

cuboidArea :: Float -&gt; Float -&gt; Float -&gt; Float  
cuboidArea a b c = rectangleArea a b * 2 + rectangleArea a c * 2 + rectangleArea c b * 2  

rectangleArea :: Float -&gt; Float -&gt; Float  
rectangleArea a b = a * b</pre><img src="../img/making_modules.png" alt="" style="float:right" class="floatright" />
<p>標準的幾何公式。有幾個地方需要注意一下，由於立方體只是長方體的特殊形式，所以在求它面積和體積的時候我們就將它當作是邊長相等的長方體。在這裡還定義了一個 <code>helper</code>函數，<code>rectangleArea</code> 它可以通過長方體的兩條邊計算出長方體的面積。它僅僅是簡單的相乘而已，份量不大。但請注意我們可以在這一模組中呼叫這個函數，而它不會被導出! 因為我們這個模組只與三維圖形打交道.</p>

<p>當構造一個模組的時候，我們通常只會導出那些行為相近的函數，而其內部的實現則是隱蔽的。如果有人用到了 <code>Geometry</code> 模組，就不需要關心它的內部實現是如何。我們作為編寫者，完全可以隨意修改這些函數甚至將其刪掉，沒有人會注意到裡面的變動，因為我們並不把它們導出.</p>

<p>要使用我們的模組，只需:</p>
<pre class="code">import Geometry</pre>
<p>將 <code>Geometry.hs</code> 檔案至于用到它的程序檔案的同一目錄之下.</p>

<p>模組也可以按照分層的結構來組織，每個模組都可以含有多個子模組。而子模組還可以有自己的子模組。我們可以把 <code>Geometry</code> 分成三個子模組，而一個模組對應各自的圖形對象.</p>

<p>首先，建立一個 <code>Geometry</code> 檔案夾，注意首字母要大寫，在裡面新建三個檔案</p>

<p>如下就是各個檔案的內容:</p>

<p>sphere.hs</p>
<pre class="code">module Geometry.Sphere  
( volume  
，area  
) where  

volume :: Float -&gt; Float  
volume radius = (4.0 / 3.0) * pi * (radius ^ 3)  

area :: Float -&gt; Float  
area radius = 4 * pi * (radius ^ 2)</pre>
<p>cuboid.hs</p>
<pre class="code">module Geometry.Cuboid  
( volume  
，area  
) where  

volume :: Float -&gt; Float -&gt; Float -&gt; Float  
volume a b c = rectangleArea a b * c  

area :: Float -&gt; Float -&gt; Float -&gt; Float  
area a b c = rectangleArea a b * 2 + rectangleArea a c * 2 + rectangleArea c b * 2  

rectangleArea :: Float -&gt; Float -&gt; Float  
rectangleArea a b = a * b</pre>
<p>cube.hs</p>
<pre class="code">module Geometry.Cube  
( volume  
，area  
) where  

import qualified Geometry.Cuboid as Cuboid  

volume :: Float -&gt; Float  
volume side = Cuboid.volume side side side  

area :: Float -&gt; Float  
area side = Cuboid.area side side side</pre>
<p>好的! 先是 <code>Geometry.Sphere</code>。注意，我們將它置於 <code>Geometry</code> 檔案夾之中並將它的名字定為 <code>Geometry.Sphere</code>。對 Cuboid 也是同樣，也注意下，在三個模組中我們定義了許多名稱相同的函數，因為所在模組不同，所以不會產生命名衝突。若要在 <code>Geometry.Cube</code> 使用 <code>Geometry.Cuboid</code> 中的函數，就不能直接 <code>import Geometry.Cuboid</code>，而必須得 <code>qualified import</code>。因為它們中間的函數名完全相同.</p>
<pre class="code">import Geometry.Sphere</pre>
<p>然後，呼叫 <code>area</code> 和 <code>volume</code>，就可以得到球體的面積和體積，而若要用到兩個或更多此類模組，就必須得 <code>qualified import</code> 來避免重名。所以就得這樣寫:</p>
<pre class="code">import qualified Geometry.Sphere as Sphere  
import qualified Geometry.Cuboid as Cuboid  
import qualified Geometry.Cube as Cube</pre>
<p>然後就可以呼叫 <code>Sphere.area</code>，<code>Sphere.volume</code>，<code>Cuboid.area</code> 了，而每個函數都只計算其對應物體的面積和體積.</p>

<p>以後你若發現自己的程式碼體積龐大且函數眾多，就應該試着找找目的相近的函數能否裝入各自的模組，也方便日後的重用.</p>

            <ul class="nav">
                <li class="left">  <img src="../img/prv.png" alt="prev" /><a href="high-order-function.html">高階函數</a></li>
                <li class="center"><a href="chapters.html">目录</a></li>
                <li class="right"> <a href="make-types-and-classes-for-ourselves.html">構造我們自己的 Types 和 Typeclasses</a><img src="../img/nxt.png" alt="next" /></li>
            </ul>
        </div>
        <div id="footer">
        </div>
        <div id="feedback">Send feedback</div>
    </body>
</html>
