<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Strict//EN"
"http://www.w3.org/TR/xhtml1/DTD/xhtml1-strict.dtd">
<html xmlns="http://www.w3.org/1999/xhtml">
    <head>
        <meta http-equiv="Content-Type" content="text/html; charset=utf-8">
        <style>
            @import url(css/style.css);
            @import url(css/feedback.css);
        </style>
        <script src="js/jquery.js"></script>
        <script src="js/jquery.chili-2.2.js"></script>
        <script src="js/script.js"></script>
        <script src="js/html2canvas.js"></script>
        <script src="js/jsfeedback.js"></script>
        <script>
             ChiliBook.recipeFolder = "js/chili/";  
             ChiliBook.automaticSelector = "pre";
        </script>
        <script>
            $(function(){
                $('#feedback').click(function(){
                    $('body').feedback();
                })
            });
        </script>
        <script type="text/javascript">
            var _gaq = _gaq || [];
            _gaq.push(['_setAccount', 'UA-32830659-1']);
            _gaq.push(['_trackPageview']);

            (function() {
                var ga = document.createElement('script'); ga.type = 'text/javascript'; ga.async = true;
                ga.src = ('https:' == document.location.protocol ? 'https://ssl' : 'http://www') + '.google-analytics.com/ga.js';
                    var s = document.getElementsByTagName('script')[0]; s.parentNode.insertBefore(ga, s);
            })();
        </script>
        <title>輸入與輸出</title>
    </head>
    <body>
        <div id="header">
            <img id="beta" alt="beta" src="img/beta.png" />
        </div>

        <div id="main">
            <ul class="nav">
                <li class="left">  <img src="img/prv.png"></img><a href="make-types-and-classes-for-ourselves.html">構造我們自己的Types和Typeclasses</a></li>
                <li class="center"><a href="chapters.html">Index</a></li>
                <li class="right"> <a href="functionally-solving-problems.html">函數式地思考來解決問題</a><img src="img/nxt.png"></img></li>
            </ul>
            <a name="輸入與輸出"></a><h1>輸入與輸出</h1><img src="img/dognap.png" style="float:right"></img><p>我們已經說明了Haskell是一個純粹函數式語言。雖說在命令式語言中我們習慣給電腦執行一連串指令，在函數式語言中我們是用定義東西的方式進行。在Haskell中，一個函數不能改變狀態，像是改變一個變數的內容。(當一個函數會改變狀態，我們說這函數是有副作用的。)在Haskell中函數唯一可以做的事是根據我們給定的參數來算出的結果。如果我們用同樣的參數呼叫兩次同一個函數，它會回傳相同的結果。儘管這從命令列語言的角度來看是蠻大的限制，我們已經看過它可以達成多麼酷的效果。在一個命令式語言中，程式語言沒辦法給你任何保證在一個簡單如印出幾個數字的函數不會同時燒掉你的房子，綁架你的狗並刮傷你車子的烤漆。例如，當我們要做一個二元樹的時候，我們並不插入一個結點來改變原有的樹。由於我們無法改變狀態，我們的函數實際上回傳了一棵新的二元樹。</p><p>函數無法改變狀態的好處是它讓我們促進了我們理解程式的容易度，但同時也造成了一個問題。假如說一個函數無法改變現實世界的狀態，那它要如何印出它所計算的結果?畢竟要告訴我們結果的話，它必須要改變輸出裝置的狀態(譬如說螢幕)，然後從螢幕傳達到我們的腦，並改變我們心智的狀態。</p><p>不要太早下結論，Haskell實際上設計了一個非常聰明的系統來處理有副作用的函數，它漂亮地將我們的程式區分成純粹跟非純粹兩部分。非純粹的部分負責跟鍵盤還有螢幕溝通。有了這區分的機制，在跟外界溝通的同時，我們還是能夠有效運用純粹所帶來的好處，像是惰性求值、容錯性跟模組性</p><a name="Hello, world!"></a><h2>Hello, world!</h2><img src="img/helloworld.png" style="float:left"></img><p>到目前為止我們都是將函數載入GHCi中來測試，像是標準函式庫中的一些函式。但現在我們要做些不一樣的，寫一個真實跟世界互動的Haskell程式。當然不例外，我們會來寫個"hello world"。</p><p>現在，我們把下一行打到你熟悉的編輯器中</p><pre class="code">main = putStrLn "hello, world"</pre><p>我們定義了一個<code>main</code>，並在裡面以<code>"hello, world"</code>為參數呼叫了<code>putStrLn</code>。看起來沒什麼大不了，但不久你就會發現它的奧妙。把這程式存成<code>helloworld.hs</code>。</p><p>現在我們將做一件之前沒做過的事: 編譯你的程式。打開你的終端並切換到包含<code>helloworld.hs</code>的目錄，並輸入下列指令。</p><pre class="code">$ ghc --make helloworld
[1 of 1] Compiling Main                 ( helloworld.hs, hellowowlrd.o )
Linking helloworld ...</pre><p>順利的話你就會得到如上的訊息，接著你便可以執行你的程式<code>./helloworld</code></p><pre class="code">$ ./helloworld
hello, world</pre><p>這就是我們第一個編譯成功並印出字串到螢幕的程式。很簡單吧。</p><p>讓我們來看一下我們究竟做了些什麼，首先來看一下<code>putStrLn</code>函數的型態</p><pre class="code">ghci> :t putStrLn
putStrLn :: String -> IO ()
ghci> :t putStrLn "hello, world"
putStrLn "hello, world" :: IO ()</pre><p>我們可以這麼解讀<code>putStrLn</code>的型態:<code>putStrLn</code>接受一個字串並回傳一個I/O action，這I/O action包含了<code>()</code>的型態。(即空的tuple，或者是unit型態)。一個I/O action是一個會造成副作用的動作，常是指讀取輸入或輸出到螢幕，同時也代表會回傳某些值。在螢幕印出幾個字串並沒有什麼有意義的回傳值可言，所以這邊用一個<code>()</code>來代表。</p><p>那究竟I/O action會在什麼時候被觸發呢?這就是<code>main</code>的功用所在。一個I/O action會在我們把它綁定到<code>main</code>這個名字並且執行程式的時候觸發。</p><p>把整個程式限制在只能有一個I/O action看似是個極大的限制。這就是為什麼我們需要do表示法來將所有I/O action綁成一個。來看看下面這個例子。</p><pre class="code">main = do
    putStrLn "Hello, what's your name?"
    name <- getLine
    putStrLn ("Hey " ++ name ++ ", you rock!")</pre><p>新的語法，有趣吧! 它看起來就像一個命令式的程式。如果你編譯並執行它，它便會照你預期的方式執行。我們寫了一個do並且接著一連串指令，就像寫個命令式程式一般，每一步都是一個I/O action。將所有I/O action用do綁在一起變成了一個大的I/O action。這個大的I/O action的型態是<code>IO ()</code>，這完全是由最後一個I/O action所決定的。</p><p>這就是為什麼<code>main</code>的型態永遠都是<code>main :: IO something</code>，其中<code>something</code>是某個具體的型態。按照慣例，我們通常是不會把<code>main</code>的型態在程式中寫出來。</p><p>另一個有趣的事情是第三行<code>name <- getLine</code>。它看起來像是從輸入讀取一行並存到一個變數<code>name</code>之中。真的是這樣嗎? 我們來看看<code>getLine</code>的型態吧</p><pre class="code">ghci> :t getLine
getLine :: IO String</pre><img src="img/luggage.png" style="float:left"></img><p>我們可以看到<code>getLine</code>是一個回傳<code>String</code>的I/O action。因為它會等使用者輸入某些字串，這很合理。那<code>name <- getLine</code>又是如何?你能這樣解讀它: 執行一個I/O action<code>getLine</code>並將它的結果綁定到<code>name</code>這個名字。<code>getLine</code>的型態是<code>IO String</code>，所以<code>name</code>的型態會是<code>String</code>。你能把I/O action想程式一個長了腳的盒子，它會跑到真實世界中替你做某些事，像是在牆壁上塗鴉，然後帶回來某些資料。一旦它帶了某些資料給你，打開盒子的唯一辦法就是用<code><-</code>。而且如果我們要從I/O action拿出某些資料，就一定同時要在另一個I/O action中。這就是Haskell如何漂亮地分開純粹跟不純粹的程式的方法。<code>getLine</code>在這樣的意義下是不純粹的，因為執行兩次的時候它沒辦法保證會回傳一樣的值。這也是為什麼它需要在一個<code>IO</code>的型態建構子中，那樣我們才能在I/O actio中取出資料。而且任何一段程式一旦依賴著I/O資料的話，那段程式也會被視為I/O code。</p><p>但這不表示我們不能在純粹的程式碼中使用I/O action回傳的資料。只要我們綁定它到一個名字，我們便可以暫時地使用它。像在<code>name <-getLine</code>中<code>name</code>不過是一個普通字串，代表在盒子中的內容。我們能將這個普通的字串傳給一個極度複雜的函數，並回傳你一生會有多少財富。像是這樣。</p><pre class="code">main = do
    putStrLn "Hello, what's your name?"
    name <- getLine
    putStrLn $ "Read this carefully, because this is your future: " ++ tellFortune name</pre><p><code>tellFortune</code>並不知道任何I/O有關的事，它的型態只不過是<code>String -> String</code>。</p><p>再來看看這段程式碼吧，他是合法的嗎?</p><pre class="code">nameTag = "Hello, my name is " ++ getLine</pre><p>如果你回答不是，恭喜你。如果你說是，你答錯了。這麼做不對的理由是<code>++</code>要求兩個參數都必須是串列。他左邊的參數是<code>String</code>，也就是<code>[Char]</code>。然而<code>getLine</code>的型態是<code>IO String</code>。你不能串接一個字串跟I/O action。我們必須先把<code>String</code>的值從I/O action中取出，而唯一可行的方法就是在I/O action中使用<code>name <- getLine</code>。如果我們需要處理一些非純粹的資料，那我們就要在非純粹的環境中做。所以我們最好把I/O的部分縮減到最小的比例。</p><p>每個I/O action都有一個值封裝在裡面。這也是為什麼我們之前的程式可以這麼寫。</p><pre class="code">main = do
    foo <- putStrLn "Hello, what's your name?"
    name <- getLine
    putStrLn ("Hey " ++ name ++ ", you rock!")</pre><p>然而，<code>foo</code>只會有一個<code>()</code>的值，所以綁定到<code>foo</code>這個名字似乎是多餘的。另外注意到我們並沒有綁定最後一行的<code>putStrLn</code>給任何名字。那是因為在一個do block中，最後一個action不能綁定任何名字。我們在之後講解Monad的時候會說明為什麼。現在你可以先想成do block會自動從最後一個action取出值並綁定給他的結果。</p><p>除了最後一行之外，其他在do中沒有綁定名字的其實也可以寫成綁定的形式。所以<code>putStrLn "BLAH"</code>可以寫成<code>_ <- putStrLn "BLAH"</code>。但這沒什麼實際的意義，所以我們寧願寫成<code>putStrLn something</code>。</p><p>初學者有時候會想錯</p><pre class="code">name = getLine</pre><p>以為這行會讀取輸入並給他榜定一個名字叫<code>name</code>但其實只是把<code>getLine</code>這個I/O action指定一個名字叫<code>name</code>罷了記住，要從一個I/O action中取出值，你必須要在另一個I/O action中將他用<code><-</code>綁定給一個名字。</p><p>I/O actions只會在綁定給<code>main</code>的時候或是在另一個用do串起來的I/O action才會執行。你可以用do來串接I/O actions，再用do來串接這些串接起來的I/O actions。不過只有最外面的I/O action被指定給main才會觸發執行。</p><p>喔對，其實還有另外一個情況。就是在GHCi中輸入一個I/O action並按下Enter鍵，那也會被執行</p><pre class="code">ghci> putStrLn "HEEY"
HEEY</pre><p>就算我們只是在GHCi中打幾個數字或是呼叫一個函數，按下Enter就會計算他並呼叫<code>show</code>，他會用<code>putStrLn</code>將字串印出的終端上。</p><p>還記得let binding嗎? 如果不記得，回去溫習一下這個章節。他們的形式是<code>let bindings in expression</code>，其中<code>bindings</code>是expression中的名字、<code>expression</code>則是被運用到這些名字的算式。我們也提到了list comprehensions中，<code>in</code>不是必須的。你能夠在do blocks中使用let bindings就像是你在list comprehensions中使用他們一樣，像這樣:</p><pre class="code">import Data.Char

main = do
    putStrLn "What's your first name?"
    firstName <- getLine
    putStrLn "What's your last name?"
    lastName <- getLine
    let bigFirstName = map toUpper firstName
        bigLastName = map toUpper lastName
    putStrLn $ "hey " ++ bigFirstName ++ " " ++ bigLastName ++ ", how are you?"</pre><p>注意我們是怎麼編排在do block中的I/O actions，也注意到我們是怎麼編排let跟其中的名字的，由於對齊在Haskell中並不會被無視，這麼編排才是好的習慣。我們的程式用<code>map toUpper firstName</code>將<code>"John"</code>轉成大寫的<code>"JOHN"</code>，並將大寫的結果綁定到一個名字上，之後在輸出的時候參考到了這個名字。</p><p>你也許會問究竟什麼時候要用<code><-</code>什麼時候用let bindings?記住，<code><-</code>是用來運算I/O actions並將他的結果綁定到名稱。而<code>map toUpper firstName</code>並不是一個I/O action。他只是一個純粹的expression。所以總結來說，當你要綁定I/O actions的結果時用<code><-</code>，而對於純粹的expression使用let bindings。對於錯誤的<code>let firstName = getLine</code>，我們只不過是把<code>getLine</code>這個I/O actions給了一個不同的名字罷了。最後還是要用<code><-</code>將結果取出。</p><p>現在我們來寫一個不斷讀取一行輸入並將這行輸入的字反過來輸出到螢幕上。程式會在輸入空白行的時候停止。</p><pre class="code">main = do
    line <- getLine
    if null line
        then return ()
        else do
            putStrLn $ revverseWords line
            main

reverseWords :: String -> String
reverseWords = unwords . map reverse . words</pre><p>你可以執行看看來得到些感覺。</p><p>首先，我們來看一下<code>reverseWords</code>。他不過是一個普通的函數，假如接受了個字串<code>"hey there man"</code>，他會先呼叫<code>words</code>來產生一個字的串列<code>["hey", "there", "man"]</code>。然後用<code>reverse</code>來map整個串列，得到<code>["yeh", "ereht", "nam"]</code>，接著用<code>unwords</code>來得到最終的結果<code>"yeh ereht nam"</code>。這些用函數合成來簡潔的表達。如果沒有用函數合成，那就會寫成醜醜的樣子<code>reverseWords st = unwords (map reverse (words st))</code></p><p>那<code>main</code>又是怎麼一回事呢?首先，我們用<code>getLine</code>從終端讀取了一行，並把這行輸入取名叫<code>line</code>。然後接著一個條件式expression。記住，在Haskell中if永遠要伴隨一個else，這樣每個expression才會有值。當if的條件是true (也就是輸入了一個空白行)，我們便執行一個I/O action，如果if的條件是false，那else底下的I/O action被執行。這也就是說當if在一個I/O do block中的時候，長的樣子是<code>if condition then I/O action else I/O action</code></p><p>我們首先來看一下在else中發生了什麼事。由於我們在else中只能有一個I/O action，所以我們用do來將兩個I/O actions綁成一個，你可以寫成這樣</p><pre class="code">else (do
    putStrLn $ reverseWords line
    main)</pre><p>這樣可以明顯看到整個do block可以看作一個I/O action，只是比較醜。但總之，在do block裡面，我們依序呼叫了<code>getLine</code>以及<code>reverseWords</code>，在那之後，我們遞迴呼叫了<code>main</code>。由於main也是一個I/O action，所以這不會造成任何問題。呼叫<code>main</code>也就代表我們回到程式的起點。</p><p>那假如<code>null line</code>的結果是true呢?也就是說then的區塊被執行。我們看一下區塊裡面有<code>then return ()</code>。如果你是從C, Java或Python過來的，你可能會想<code>return</code>不過是作一樣的事情便跳過這一段。但很重要的:<code>return</code>在Hakell裡面的意義跟其他語言的<code>return</code>完全不同！他們有相同的樣貌，造成了許多人搞錯，但確實他們是不一樣的。在命令式語言中，<code>return</code>通常結束method或subroutine的執行，並且回傳某個值給呼叫者。在Haskell中，他的意義則是利用某個pure value造出I/O action。用之前盒子的比喻來說，就是將一個value裝進箱子裡面。產生出的I/O action並沒有作任何事，只不過將value包起來而已。所以在I/O的情況下來說，<code>return "haha"</code>的型態是<code>IO String</code>。將pure value包成I/O action有什麼實質意義呢？為什麼要弄成<code>IO</code>包起來的值？這是因為我們一定要在else中擺上某些I/O action，所以我們才用<code>return ()</code>做了一個沒作什麼事情的I/O action。</p><p>在I/O do block中放一個<code>return</code>並不會結束執行。像下面這個程式會執行到底。</p><pre class="code">main = do
    return ()
    return "HAHAHA"
    line <- getLine
    return "BLAH BLAH BLAH"
    return 4
    putStrLn line</pre><p>所有在程式中的<code>return</code>都是將value包成I/O actions，而且由於我們沒有將他們綁定名稱，所以這些結果都被忽略。我們能用<code><-</code>與<code>return</code>來達到綁定名稱的目的。</p><pre class="code">main = do
    a <- return "hell"
    b <- return "yeah!"
    putStrLn $ a ++ " " ++ b</pre><p>可以看到<code>return</code>與<code><-</code>作用相反。<code>return</code>把value裝進盒子中，而<code><-</code>將value從盒子拿出來，並綁定一個名稱。不過這麼作是有些多餘，因為你可以用let bindings來綁定</p><pre class="code">main = do
    let a = "hell"
        b = "yeah"
    putStrLn $ a ++ " " ++ b</pre><p>在I/O do block中需要<code>return</code>的原因大致上有兩個，一個是我們需要一個什麼事都不做的I/O action，或是我們不希望這個do block形成的I/O action的結果值是這個block中的最後一個I/O action，我們希望有一個不同的結果值，所以我們用<code>return</code>來作一個I/O action包了我們想要的結果放在do block的最後。</p><p>在我們接下去講檔案之前，讓我們來看看有哪些實用的函數可以處理I/O。</p><p><code>putStr</code>跟<code>putStrLn</code>幾乎一模一樣，都是接受一個字串當作參數，並回傳一個I/O action印出字串到終端上，只差在<code>putStrLn</code>會換行而<code>putStr</code>不會罷了。</p><pre class="code">main = do putStr "Hey, "
          putStr "I'm "
          putStrLn "Andy!"</pre><pre class="code">$ runhaskell putstr_test.hs
Hey, I'm Andy!</pre><p>他的type signature是<code>putStr :: String -> IO ()</code>，所以是一個包在I/O action中的unit。也就是空值，沒有辦法綁定他。</p><p><code>putChar</code>接受一個字元，並回傳一個I/O action將他印到終端上。</p><pre class="code">main = do putChar 't'    
          putChar 'e'
          putChar 'h'</pre><pre class="code">$ runhaskell putchar_test.hs
teh</pre><p><code>putStr</code>實際上就是<code>putChar</code>遞迴定義出來的。<code>putStr</code>的邊界條件是空字串，所以假設我們印一個空字串，那他只是回傳一個什麼都不做的I/O action，像<code>return ()</code>。如果印的不是空字串，那就先用<code>putChar</code>印出字串的第一個字元，然後再用<code>putStr</code>印出字串剩下部份。</p><pre class="code">putStr :: String -> IO ()
putStr [] = return ()
putStr (x:xs) = do
    putChar x
    putStr xs</pre><p>看看我們如何在I/O中使用遞迴，就像我們在pure code中所做的一樣。先定義一個邊界條件，然後再思考剩下如何作。</p><p><code>print</code>接受任何是<code>Show</code>typeclass的instance的型態的值，這代表我們知道如何用字串表示他，呼叫<code>show</code>來將值變成字串然後將其輸出到終端上。基本上，他就是<code>putStrLn . show</code>。首先呼叫<code>show</code>然後把結果餵給<code>putStrLn</code>，回傳一個I/O action印出我們的值。</p><pre class="code">main = do print True
          print 2
          print "haha"
          print 3.2
          print [3,4,3]</pre><pre class="code">$ runhaskell print_test.hs
True
2
"haha"
3.2
[3,4,3]</pre><p>就像你看到的，這是個很方便的函數。還記得我們提到I/O actions只有在<code>main</code>中才會被執行以及在GHCI中運算的事情嗎？當我們用鍵盤打了些值，像<code>3</code>或<code>[1,2,3]</code>並按下Enter，GHCI實際上就是用了<code>print</code>來將這些值輸出到終端。</p><pre class="code">ghci> 3
3
ghci> print 3
3
ghci> map (++"!") ["hey","ho","woo"]
["hey!","ho!","woo!"]
ghci> print (map (++"!") ["hey", "ho", "woo"])
["hey!","ho!","woo!"]</pre><p>當我們需要印出字串，我們會用<code>putStrLn</code>，因為我們不想要周圍有括號，但對於輸出值來說，<code>print</code>才是最常用的。</p><p><code>getChar</code>是一個從輸入讀進一個字元的I/O action，因此他的type signature是<code>getChar :: IO Char</code>，代表一個I/O action的結果是<code>Char</code>。注意由於緩衝區的關係，只有當Enter被按下的時候才會觸發讀取字元的行為。</p><pre class="code">main = do
    c <- getChar
    if c /= ' '
        then do
            putChar c
            main
        else return ()</pre><p>這程式看起來像是讀取一個字元並檢查他是否為一個空白。如果是的話便停止，如果不是的話便印到終端上並重複之前的行為。在某種程度上來說也不能說錯，只是結果不如你預期而已。來看看結果吧。</p><pre class="code">$ runhaskell getchar_test.hs
hello sir
hello</pre><p>上面的第二行是輸入。我們輸入了<code>hello sir</code>並按下了Enter。由於緩衝區的關係，程式是在我們按了Enter後才執行而不是在某個輸入字元的時候。一旦我們按下了Enter，那他就把我們直到目前輸入的一次做完。</p><p><code>when</code>這函數可以在<code>Control.Monad</code>中找到他 (你必須<code>import Contorl.Monad</code>才能使用他)。他在一個do block中看起來就像一個控制流程的statement，但實際上他的確是一個普通的函數。他接受一個boolean值跟一個I/O action。如果boolean值是<code>True</code>，便回傳我們傳給他的I/O action。如果boolean值是<code>False</code>，便回傳<code>return ()</code>，即什麼都不做的I/O action。我們接下來用<code>when</code>來改寫我們之前的程式。</p><pre class="code">import Control.Monad

main = do
    c <- getChar
    when (c /= ' ') $ do
        putChar c
        main</pre><p>就像你看到的，他可以將<code>if something then do some I/O action else return ()</code>這樣的模式封裝起來。</p><p><code>sequence</code>接受一串I/O action，並回傳一個會依序執行他們的I/O action。運算的結果是包在一個I/O action的一連串I/O action的運算結果。他的type signature是<code>sequence :: [IO a] -> IO [a]</code></p><pre class="code">main = do
    a <- getLine
    b <- getLine
    c <- getLine
    print [a,b,c]</pre><p>其實可以寫成</p><pre class="code">main = do
    rs <- sequence [getLine, getLine, getLine]
    print rs</pre><p>所以<code>sequence [getLine, getLine, getLine]</code>作成了一個執行<code>getLine</code>三次的I/O action。如果我們對他綁定一個名字，結果便是這串結果的串列。也就是說，三個使用者輸入的東西組成的串列。</p><p>一個常見的使用方式是我們將<code>print</code>或<code>putStrLn</code>之類的函數map到串列上。<code>map print [1,2,3,4]</code>這個動作並不會產生一個I/O action，而是一串I/O action，就像是<code>[print 1, print 2, print 3, print 4]</code>。如果我們將一串I/O action變成一個I/O action，我們必須用<code>sequence</code></p><pre class="code">ghci> sequence (map print [1,2,3,4,5])    
1
2
3
4
5
[(),(),(),(),()]</pre><p>那<code>[(),(),(),(),()]</code>是怎麼回事？當我們在GHCI中運算I/O action，他會被執行並把結果印出來，唯一例外是結果是<code>()</code>的時候不會被印出。這也是為什麼<code>putStrLn "hehe"</code>在GHCI中只會印出<code>hehe</code>(因為<code>putStrLn "hehe"</code>的結果是<code>()</code>)。但當我們使用<code>getLine</code>時，由於<code>getLine</code>的型態是<code>IO String</code>，所以結果會被印出來。</p><p>由於對一個串列map一個回傳I/O action的函數，然後再sequence他這個動作太常用了。所以有一些函數在函式庫中<code>mapM</code>跟<code>mapM_</code>。<code>mapM</code>接受一個函數跟一個串列，將對串列用函數map然後sequence結果。<code>mapM_</code>也作同樣的事，只是他把運算的結果丟掉而已。在我們不關心I/O action結果的情況下，<code>mapM_</code>是最常被使用的。</p><pre class="code">ghci> mapM print [1,2,3]    
1
2
3
[(),(),()]
ghci> mapM_ print [1,2,3]
1
2
3</pre><p><code>forever</code>接受一個I/O action並回傳一個永遠作同一件事的I/O action。你可以在<code>Control.Monad</code>中找到他。下面的程式會不斷地要使用者輸入些東西，並把輸入的東西轉成大寫輸出到螢幕上。</p><pre class="code">import Control.Monad
import Data.Char

main = forever $ do
    putStr "Give me some input: "
    l <- getLine
    putStrLn $ map toUpper l</pre><p>在<code>Control.Monad</code>中的<code>forM</code>跟<code>mapM</code>的作用一樣，只是參數的順序相反而已。第一個參數是串列，而第二個則是函數。這有什麼用？在一些有趣的情況下還是有用的：</p><pre class="code">import Control.Monad

main = do 
    colors <- forM [1,2,3,4] (\a -> do
        putStrLn $ "Which color do you associate with the number " ++ show a ++ "?"
        color <- getLine
        return color)
    putStrLn "The colors that you associate with 1, 2, 3 and 4 are: "
    mapM putStrLn colors</pre><p><code>(\a -> do ...)</code>是接受一個數字並回傳一個I/O action的函數。我們必須用括號括住他，不然lambda會貪心match的策略會把最後兩個I/O action也算進去。注意我們在do block裡面<code>return color</code>。我們那麼作是讓do block的結果是我們選的顏色。實際上我們並不需那麼作，因為<code>getLine</code>已經達到我們的目的。先<code>color <- getLine</code>再<code>return color</code>只不過是把值取出再包起來，其實是跟<code>getLine</code>效果相當。<code>forM</code>產生一個I/O action，我們把結果綁定到<code>colors</code>這名稱。<code>colors</code>是一個普通包含字串的串列。最後，我們用<code>mapM putStrLn colors</code>印出所有顏色。</p><p>你可以把<code>forM</code>的意思想成將串列中的每個元素作成一個I/O action。至於每個I/O action實際作什麼就要看原本的元素是什麼。然後，執行這些I/O action並將結果綁定到某個名稱上。或是直接將結果忽略掉。</p><pre class="code">$ runhaskell from_test.hs
Which color do you associate with the number 1?  
white  
Which color do you associate with the number 2?  
blue  
Which color do you associate with the number 3?  
red  
Which color do you associate with the number 4?  
orange  
The colors that you associate with 1, 2, 3 and 4 are:  
white  
blue  
red  
orange</pre><p>其實我們也不是一定要用到<code>forM</code>，只是用了<code>forM</code>程式會比較容易理解。正常來講是我們需要在map跟sequence的時候定義I/O action的時候使用<code>forM</code>，同樣地，我們也可以將最後一行寫成<code>forM colors putStrLn</code></p><p>在這一節，我們學會了輸入與輸出的基礎。我們也了解了什麼是I/O action，他們是如何幫助我們達成輸入與輸出的目的。這邊重複一遍，I/O action跟其他Haskell中的value沒有兩樣。我們能夠把他當參數傳給函式，或是函式回傳I/O action。他們特別之處在於當他們是寫在<code>main</code>裡面或GHCI裡面的時候，他們會被執行，也就是實際輸出到你螢幕或輸出音效的時候。每個I/O action也能包著一個從真實世界拿回來的值。</p><p>不要把像是<code>putStrLn</code>的函式想成接受字串並輸出到螢幕。要想成一個函式接受字串並回傳一個I/O action。當I/O action被執行的時候，會漂亮地印出你想要的東西。</p><a name="檔案與字符流"></a><h2>檔案與字符流</h2><img src="img/streams.png" style="float:right"></img><p><code>getChar</code>是一個讀取單一字元的I/O action。<code>getLine</code>是一個讀取一行的I/O action。這是兩個非常直覺的函式，多數程式語言也有類似這兩個函式的statement或function。但現在我們來看看<b>getContents</b>。<code>getContents</code>是一個從標準輸入讀取直到end-of-file字元的I/O action。他的型態是<code>getContents :: IO String</code>。最酷的是<code>getContents</code>是惰性I/O (Lazy I/O)。當我們寫了<code>foo <- getContents</code>，他並不會馬上讀取所有輸入，將他們存在memory裡面。他只有當你真的需要輸入資料的時候才會讀取。</p><p>當我們需要重導一個程式的輸出到另一個程式的輸入時，<code>getContents</code>非常有用。假設我們有下面一個文字檔。</p><pre class="code">I'm a lil' teapot  
What's with that airplane food, huh?  
It's so small, tasteless</pre><p>還記得我們介紹<code>forever</code>時寫的小程式嗎？會把所有輸入的東西轉成大寫的那一個。為了防止你忘記了，這邊再重複一遍。</p><pre class="code">import Control.Monad  
import Data.Char  
  
main = forever $ do  
    putStr "Give me some input: "  
    l <- getLine  
    putStrLn $ map toUpper l</pre><p>將我們的程式存成<code>capslocker.hs</code>然後編譯他。然後用Unix的Pipe將文字檔餵給我們的程式。我們使用的是GNU的cat，會將指定的檔案輸出到螢幕。</p><pre class="code">$ ghc --make capslocker   
[1 of 1] Compiling Main             ( capslocker.hs, capslocker.o )  
Linking capslocker ...  
$ cat haiku.txt  
I'm a lil' teapot  
What's with that airplane food, huh?  
It's so small, tasteless  
$ cat haiku.txt | ./capslocker  
I'M A LIL' TEAPOT  
WHAT'S WITH THAT AIRPLANE FOOD, HUH?  
IT'S SO SMALL, TASTELESS  
capslocker <stdin>: hGetLine: end of file</pre><p>就如你看到的，我們是用<code>|</code>這符號來將某個程式的輸出piping到另一個程式的輸入。我們做的事相當於run我們的capslocker，然後將haiku的內容用鍵盤打到終端上，最後再按Ctrl-D來代表end-of-file。這就像執行cat haiku.txt後大喊，嘿，不要把內容印到終端上，把內容塞到capslocker!</p><p>我們用<code>forever</code>在做的事基本上就是將輸入經過轉換後變成輸出。用<code>getContents</code>的話可以讓我們的程式更加精練。</p><pre class="code">import Data.Char

main = do
    contents <- getContents
    putStr (map toUpper contents)</pre><p>我們將<code>getContents</code>取回的字串綁定到<code>contents</code>。然後用<code>toUpper</code>map到整個字串後印到終端上。記住字串基本上就是一串惰性的串列(list)，同時<code>getContents</code>也是惰性I/O，他不會一口氣讀入內容然後將內容存在記憶體中。實際上，他會一行一行讀入並輸出大寫的版本，這是因為輸出才是真的需要輸入的資料的時候。</p><pre class="code">$ cat haiku.txt | ./capslocker
I'M A LIL' TEAPOT
WHAT'S WITH THAT AIRPLAN FOOD, HUH?
IT'S SO SMALL, TASTELESS</pre><p>很好，程式運作正常。假如我們執行capslocker然後自己打幾行字呢？</p><pre class="code">$ ./capslocker
hey ho
HEY HO
lets go
LETS GO</pre><p>按下Ctrl-D來離開環境。就像你看到的，程式是一行一行將我們的輸入印出來。當<code>getContent</code>的結果被綁定到<code>contents</code>的時候，他不是被表示成在記憶體中的一個字串，反而比較像是他有一天會是字串的一個承諾。當我們將<code>toUpper</code>map到<code>contents</code>的時候，便也是一個函數被承諾將會被map到內容上。最後<code>putStr</code>則要求先前的承諾說，給我一行大寫的字串吧。實際上還沒有任何一行被取出，所以便跟<code>contents</code>說，不如從終端那邊取出些姿串吧。這才是<code>getContents</code>真正從終端讀入一行並把這一行交給程式的時候。程式便將這一行用<code>toUpper</code>處裡並交給<code>putStr</code>，<code>putStr</code>則印出他。之後<code>putStr</code>再說：我需要下一行。整個步驟便再重複一次，直到讀到end-of-file為止。</p><p>接著我們來寫個程式，讀取輸入，並只印出少於十個字元的行。</p><pre class="code">main = do
    contents <- getContents
    putStr (shortLinesOnly contents)

shortLinesOnly :: String -> String
shortLinesOnly input = 
    let allLines = lines input
        shortLines = filter (\line -> length line < 10) allLines
        result = unlines shortLines
    in result</pre><p>我們把I/O部份的程式碼弄得很短。由於程式的行為是接某些輸入，作些處裡然後輸出。我們可以把他想成讀取輸入，呼叫一個函數，然後把函數的結果輸出。</p><p><code>shortLinesOnly</code>的行為是這樣：拿到一個字串，像是<code>"short\nlooooooooooooooong\nshort again"</code>。這字串有三行，前後兩行比較短，中間一行很常。他用<code>lines</code>把字串分成<code>["short", "looooooooooooooong", "short again"]</code>，並把結果綁定成<code>allLines</code>。然後過濾這些字串，只有少於十個字元的留下，<code>["short", "short again"]</code>，最後用<code>unlines</code>把這些字串用換行接起來，形成<code>"short\nshort again"</code></p><pre class="code">i'm short  
so am i  
i am a loooooooooong line!!!  
yeah i'm long so what hahahaha!!!!!!  
short line  
loooooooooooooooooooooooooooong  
short</pre><pre class="code">$ ghc --make shortlinesonly  
[1 of 1] Compiling Main             ( shortlinesonly.hs, shortlinesonly.o )  
Linking shortlinesonly ...  
$ cat shortlines.txt | ./shortlinesonly  
i'm short  
so am i  
short</pre><p>我們把shortlines.txt的內容經由pipe送給shortlinesonly，結果就如你看到，我們只有得到比較短的行。</p><p>從輸入那一些字串，經由一些轉換然後輸出這樣的模式實在太常用了。常用到甚至建立了一個函數叫<strong>interact</strong>。<code>interact</code>接受一個<code>String -> String</code>的函數，並回傳一個I/O action。那個I/O action會讀取一些輸入，呼叫提供的函數，然後把函數的結果印出來。所以我們的程式可以改寫成這樣。</p><pre class="code">main = interact shortLinesOnly

shortLinesOnly :: String -> String
shortLinesOnly input = 
    let allLines = lines input
        shortLines = filter (\line -> length line < 10) allLines
        result = unlines shortLines
    in result</pre><p>我們甚至可以再讓程式碼更短一些，像這樣</p><pre class="code">main = interact $ unlines . filter ((<10) . length) . lines</pre><p>看吧，我們讓程式縮到只剩一行了，很酷吧！</p><p>能應用<code>interact</code>的情況有幾種，像是從輸入pipe進一些內容然後丟出一些結果的程式，或是從使用者獲取一行一行的輸入，然後丟回根據那一行運算的結果，再拿取另一行。這兩者的差別主要是取決於使用者使用他們的方式。</p><p>我們再來寫另一個程式，它不斷地讀取一行行並告訴我們那一行字串是不是一個回文字串(palindrome)。我們當然可以用<code>getLine</code>讀取一行然後再呼叫<code>main</code>作同樣的事。不過同樣的事情可以用<code>interact</code>更簡潔地達成。當使用<code>interact</code>的時候，想像你是將輸入經有某些轉換成輸出。在這個情況當中，我們要將每一行輸入轉換成<code>"palindrome"</code>或<code>"not a palindrome"</code>。所以我們必須寫一個函數將<code>"elephant\nABCBA\nwhatever"</code>轉換成<code>not a palindrome\npalindrome\nnot a palindrome"</code>。來動手吧！</p><pre class="code">respondPalindromes contents = unlines (map \xs ->
    if isPalindrome xs then "palindrome" else "not a palindrome") (lines contents))
        where isPalindrome xs = xs == reverse xs</pre><p>再來將程式改寫成point-free的形式</p><pre class="code">respondPalindromes = unlines . map (\xs ->
    if isPalindrome xs then "palindrome" else "not a palindrome") . lines
        where isPalindrome xs = xs == reverse xs</pre><p>很直覺吧！首先將<code>"elephant\nABCBA\nwhatever"</code>變成<code>["elephant", "ABCBA", "whatever"]</code>然後將一個lambda函數map它，<code>["not a palindrome", "palindrome", "not a palindrome"]</code>然後用<code>unlines</code>變成一行字串。接著</p><pre class="code">main = interact respondPalindromes</pre><p>來測試一下吧。</p><pre class="code">$ runhaskell palindrome.hs
hehe
not a palindrome
ABCBA
palindrome
cookie
not a palindrome</pre><p>即使我們的程式是把一大把字串轉換成另一個，其實他表現得好像我們是一行一行做的。這是因為Haskell是惰性的，程式想要印出第一行結果時，他必須要先有第一行輸入。所以一旦我們給了第一行輸入，他便印出第一行結果。我們用end-of-line字元來結束程式。</p><p>我們也可以用pipe的方式將輸入餵給程式。假設我們有這樣一個檔案。</p><pre class="code">dogaroo
radar
rotor
madam</pre><p>將他存為<code>words.txt</code>，將他餵給程式後得到的結果</p><pre class="code">$ cat words.txt | runhaskell palindromes.hs
not a palindrome
palindrome
palindrome
palindrome</pre><p>再一次地提醒，我們得到的結果跟我們自己一個一個字打進輸入的內容是一樣的。我們看不到<code>palindrome.hs</code>輸入的內容是因為內容來自於檔案。</p><p>你應該大致了解Lazy I/O是如何運作，並能善用他的優點。他可以從輸入轉換成輸出的角度方向思考。由於Lazy I/O，沒有輸入在被用到之前是真的被讀入。</p><p>到目前為止，我們的示範都是從終端讀取某些東西或是印出某些東西到終端。但如果我們想要讀寫檔案呢？其實從某個角度來說我們已經作過這件事了。我們可以把讀寫終端想成讀寫檔案。只是把檔案命名成<code>stdout</code>跟<code>stdin</code>而已。他們分別代表標準輸出跟標準輸入。我們即將看到的讀寫檔案跟讀寫終端並沒什麼不同。</p><p>首先來寫一個程式，他會開啟一個叫girlfriend.txt的檔案，檔案裡面有Avril Lavigne的暢銷名曲Girlfriend，並將內容印到終端上。接下來是girlfriend.txt的內容。</p><pre class="code">Hey! Hey! You! You!   
I don't like your girlfriend!   
No way! No way!   
I think you need a new one!</pre><p>這則是我們的主程式。</p><pre class="code">import System.IO  
  
main = do  
    handle <- openFile "girlfriend.txt" ReadMode  
    contents <- hGetContents handle  
    putStr contents  
    hClose handle</pre><p>執行他後得到的結果。</p><pre class="code">$ runhaskell girlfriend.hs  
Hey! Hey! You! You!  
I don't like your girlfriend!  
No way! No way!  
I think you need a new one!</pre><p>我們來一行行看一下程式。我們的程式用do把好幾個I/O action綁在一起。在do block的第一行，我們注意到有一個新的函數叫<strong>openFile</strong>。他的type signature是<code>openFile :: FilePath -> IOMode -> IO Handle</code>。他說了<code>openFile</code>接受一個檔案路徑跟一個<code>IOMode</code>，並回傳一個I/O action，他會打開一個檔案並把檔案關聯到一個handle。</p><p><code>FilePath</code>不過是<code>String</code>的type synonym。</p><pre class="code">type FilePath = String</pre><p><code>IOMode</code>則是一個定義如下的型態</p><pre class="code">data IOMode = ReadMode | WriteMode | AppendMode | ReadWriteMode</pre><img src="img/file.png" style="float:left"></img><p>就像我們之前定義的型態，分別代表一個星期的七天。這個型態代表了我們想對打開的檔案做什麼。很簡單吧。留意到我們的型態是<code>IOMode</code>而不是<code>IO Mode</code>。<code>IO Mode</code>代表的是一個I/O action包含了一個型態為<code>Mode</code>的值，但<code>IOMode</code>不過是一個陽春的enumeration。</p><p>最後，他回傳一個I/O action會將指定的檔案用指定的模式打開。如果我們將I/O action綁定到某個東西，我們會得到一個<code>Handle</code>。型態為<code>Handle</code>的值代表我們的檔案在哪裡。有了handle我們才知道要從哪個檔案讀取內容。想讀取檔案但不將檔案綁定到handle上這樣做是很蠢的。所以，我們將一個handle綁定到<code>handle</code>。</p><p>接著一行，我們看到一個叫<strong>hGetContents</strong>的函數。他接了一個<code>Handle</code>，所以他知道要從哪個檔案讀取內容並回傳一個<code>IO String</code>。一個包含了檔案內容的I/O action。這函數跟<code>getContents</code>差不多。唯一的差別是<code>getContents</code>會自動從標準輸入讀取內容（也就是終端），而<code>hGetContents</code>接了一個file handle，這file handle告訴他讀取哪個檔案。除此之外，他們都是一樣的。就像<code>getContents</code>，<code>hGetContents</code>不會把檔案一次都拉到記憶體中，而是有必要才會讀取。這非常酷，因為我們把<code>contents</code>當作是整個檔案般用，但他實際上不在記憶體中。就算這是個很大的檔案，<code>hGetContents</code>也不會塞爆你的記憶體，而是只有必要的時候才會讀取。</p><p>要留意檔案的handle還有檔案的內容兩個概念的差異，在我們的程式中他們分別被綁訂到<code>handle</code>跟<code>contents</code>兩個名字。handle是我們拿來區分檔案的依據。如果你把整個檔案系統想成一本厚厚的書，每個檔案分別是其中的一個章節，handle就像是書籤一般標記了你現在正在閱讀（或寫入）哪一個章節，而內容則是章節本身。</p><p>我們使用<code>putStr contents</code>印出內容到標準輸出，然後我們用了<strong>hClose</strong>。他接受一個handle然後回傳一個關掉檔案的I/O action。在用了<code>openFile</code>之後，你必須自己把檔案關掉。</p><p>要達到我們目的的另一種方式是使用<strong>withFile</strong>，他的type signature是<code>withFile :: FilePath -> IOMode -> (Handle -> IO a) -> IO a</code>。他接受一個檔案路徑，一個<code>IOMode</code>以及一個函數，這函數則接受一個handle跟一個I/O action。<code>withFile</code>最後回傳一個會打開檔案，對檔案作某件事然後關掉檔案的I/O action。處裡的結果是包在最後的I/O action中，這結果跟我們給的函數的回傳是相同的。這聽起來有些複雜，但其實很簡單，特別是我們有lambda，來看看我們用<code>withFile</code>改寫前面程式的一個範例：</p><pre class="code">import System.IO     
    
main = do     
    withFile "girlfriend.txt" ReadMode (\handle -> do  
            contents <- hGetContents handle     
            putStr contents)</pre><p>正如你看到的，程式跟之前的看起來很像。<code>(\handle -> ... )</code>是一個接受handle並回傳I/O action的函數，他通常都是用lambda來表示。我們需要一個回傳I/O action的函數的理由而不是一個本身作處裡並關掉檔案的I/O action，是因為這樣一來那個I/O action不會知道他是對哪個檔案在做處裡。用<code>withFile</code>的話，<code>withFile</code>會打開檔案並把handle傳給我們給他的函數，之後他則拿到一個I/O action，然後作成一個我們描述的I/O action，最後關上檔案。例如我們可以這樣自己作一個<code>withFile</code>：</p><pre class="code">withFile' :: FilePath -> IOMode -> (Handle -> IO a) -> IO a  
withFile' path mode f = do  
    handle <- openFile path mode   
    result <- f handle  
    hClose handle  
    return result</pre><img src="img/edd.png" style="float:right"></img><p>我們知道要回傳的是一個I/O action，所以我們先放一個do。首先我們打開檔案，得到一個handle。然後我們apply<code>handle</code>到我們的函數，並得到一個做事的I/O action。我們綁定那個I/O action到<code>result</code>這個名字，關上handle並<code>return result</code>。<code>return</code>的作用把從<code>f</code>得到的結果包在I/O action中，這樣一來I/O action中就包含了<code>f handle</code>得到的結果。如果<code>f handle</code>回傳一個從標準輸入讀去數行並寫到檔案然後回傳讀入的行數的I/O action，在<code>withFile'</code>的情形中，最後的I/O action就會包含讀入的行數。</p><p>就像<code>hGetContents</code>對應<code>getContents</code>一樣，只不過是針對某個檔案。我們也有<strong>hGetLine</strong>,<strong>hPutStr</strong>,<strong>hPutStrLn</strong>,<b>hGetChar</b>*等等。他們分別是少了h的那些函數的對應。只不過他們他們要多拿一個handle當參數，並是針對特定檔案而不是標準輸出或標準輸入。像是<code>putStrLn</code>是一個接受一個字串並回傳一個印出加了換行字元的字串的I/O action的函數。<code>hPutStrLn</code>接受一個handle跟一個字串，回傳一個印出加了換字元的字串到檔案的I/O action。以此類推，<code>hGetLine</code>接受一個handle然後回傳一個從檔案讀取一行的I/O action。</p><p>讀取檔案並對他們的字串內容作些處裡實在太常見了，常見到我們有三個函數來更進一步簡化我們的工作。</p><p><strong>readFile</strong>的type signature是<code>readFile :: FilePath -> IO String</code>。記住，<code>FilePath</code>不過是<code>String</code>的一個別名。<code>readFile</code>接受一個檔案路徑，回傳一個惰性讀取我們檔案的I/O action。然後將檔案的內容綁定到某個字串。他比起先<code>openFile</code>，綁定handle，然後<code>hGetContents</code>要好用多了。這邊是一個用<code>readFile</code>改寫之前例子的範例：</p><pre class="code">import System.IO  
  
main = do  
    contents <- readFile "girlfriend.txt"  
    putStr contents</pre><p>由於我們拿不到handle，所以我們也無法關掉他。這件事Haskell的<code>readFile</code>在背後幫我們做了。</p><p><strong>writeFile</strong>的型態是<code>writefile :: FilePath -> String -> IO ()</code>。他接受一個檔案路徑，以及一個要寫到檔案中的字串，並回傳一個寫入動作的I/O action。如果這個檔案已經存在了，他會先把檔案內容都砍了再寫入。我們是反如何把girlfriend.txt的檔案轉成大寫然後寫入到girlfriendcaps.txt中</p><pre class="code">import System.IO     
import Data.Char  
    
main = do     
    contents <- readFile "girlfriend.txt"     
    writeFile "girlfriendcaps.txt" (map toUpper contents)</pre><pre class="code">$ runhaskell girlfriendtocaps.hs  
$ cat girlfriendcaps.txt  
HEY! HEY! YOU! YOU!  
I DON'T LIKE YOUR GIRLFRIEND!  
NO WAY! NO WAY!  
I THINK YOU NEED A NEW ONE!</pre><p><strong>appendFile</strong>的型態很像<code>writeFile</code>，只是<code>appendFile</code>並不會在檔案存在時把檔案內容砍掉而是接在後面。</p><p>假設我們有一個檔案叫todo.txt，裡面每一行是一件要做的事情。現在我們寫一個程式，從標準輸入接受一行將他加到我們的to-do list中。</p><pre class="code">import System.IO     
    
main = do     
    todoItem <- getLine  
    appendFile "todo.txt" (todoItem ++ "\n")</pre><pre class="code">$ runhaskell appendtodo.hs  
Iron the dishes  
$ runhaskell appendtodo.hs  
Dust the dog  
$ runhaskell appendtodo.hs  
Take salad out of the oven  
$ cat todo.txt  
Iron the dishes  
Dust the dog  
Take salad out of the oven</pre><p>由於<code>getLine</code>回傳的值不會有換行字元，我們需要在每一行最後加上<code>"\n"</code>。</p><p>還有一件事，我們提到<code>contents <- hGetContents handle</code>是惰性I/O，不會將檔案一次都讀到記憶體中。</p><p>所以像這樣寫的話：</p><pre class="code">main = do   
    withFile "something.txt" ReadMode (\handle -> do  
        contents <- hGetContents handle  
        putStr contents)</pre><p>實際上像是用一個pipe把檔案弄到標準輸出。正如你可以把list想成stream一樣，你也可以把檔案想成stream。他會每次讀一行然後印到終端上。你也許會問這個pipe究竟一次可以塞多少東西，讀去硬碟的頻率究竟是多少？對於文字檔而言，預設的buffer通常是line-buffering。這代表一次被讀進來的大小是一行。這也是為什麼在這個case我們是一行一行處裡。對於binary file而言，預設的buffer是block-buffering。這代表我們是一個chunk一個chunk去讀得。而一個chunk的大小是根據作業系統不同而不同。</p><p>你能用<code>hSetBuffering</code>來控制buffer的行為。他接受一個handle跟一個<code>BufferMode</code>，回傳一個會設定buffer行為的I/O action。<code>BufferMode</code>是一個enumeration型態，他可能的值有：<code>NoBuffering</code>,<code>LineBuffering</code>或<code>BlockBuffering (Maybe Int)</code>。其中<code>Maybe Int</code>是表示一個chunck有幾個byte。如果他的值是<code>Nothing</code>，則作業系統會幫你決定chunk的大小。<code>NoBuffering</code>代表我們一次讀一個character。一般來說<code>NoBuffering</code>的表現很差，因為他存取硬碟的頻率很高。</p><p>接下來是我們把之前的範例改寫成用2048 bytes的chunk讀取，而不是一行一行讀。</p><pre class="code">main = do   
    withFile "something.txt" ReadMode (\handle -> do  
        hSetBuffering handle $ BlockBuffering (Just 2048)  
        contents <- hGetContents handle  
        putStr contents)</pre><p>用更大的chunk來讀取對於減少存取硬碟的次數是有幫助的，特別是我們的檔案其實是透過網路來存取。</p><p>我們也可以使用<strong>hFlush</strong>，他機受一個handle並回傳一個會flush buffer到檔案的I/O action。當我們使用line-buffering的時候，buffer在每一行都會被flush到檔案。當我們使用block-buffering的時候，是在我們讀每一個chunk作flush的動作。flush也會發生在關閉handle的時候。這代表當我們碰到換行字元的時候，讀或寫的動作都會停止並回報手邊的資料。但我們能使用<code>hFlush</code>來強迫回報所有已經在buffer中的 料。經過flushing之後，資料也就能被其他程式看見。</p><p>把block-buffering的讀取想成這樣：你的馬桶會在水箱有一加侖的水的時候自動沖水。所以你不斷灌水進去直到一加侖，馬桶就會自動沖水，在水裡面的資料也就會被看到。但你也可以手動地按下沖水鈕來沖水。他會讓現有的水被沖走。沖水這個動作就是<code>hFlush</code>這個名字的含意。</p><p>我們已經寫了一個將item加進to-do list裡面的程式，現在我們想加進移除item的功能。我先把程式碼貼上然後講解他。我們會使用一些新面孔像是<code>System.Directory</code>以及<code>System.IO</code>裡面的函數。</p><p>來看一下我們包含移除功能的程式:</p><pre class="code">import System.IO  
import System.Directory  
import Data.List  
  
main = do        
    handle <- openFile "todo.txt" ReadMode  
    (tempName, tempHandle) <- openTempFile "." "temp"  
    contents <- hGetContents handle  
    let todoTasks = lines contents     
    numberedTasks = zipWith (\n line -> show n ++ " - " ++ line) [0..] todoTasks     
    putStrLn "These are your TO-DO items:"  
    putStr $ unlines numberedTasks  
    putStrLn "Which one do you want to delete?"     
    numberString <- getLine     
    let number = read numberString     
    newTodoItems = delete (todoTasks !! number) todoTasks     
    hPutStr tempHandle $ unlines newTodoItems  
    hClose handle  
    hClose tempHandle  
    removeFile "todo.txt"  
    renameFile tempName "todo.txt"</pre><p>一開始，我們用read mode打開todo.txt，並把他綁定到<code>handle</code>。</p><p>接著，我們使用了一個之前沒用過在<code>System.IO</code>中的函數<strong>openTempFile</strong>。他的名字淺顯易懂。他接受一個暫存的資料夾跟一個樣板檔案名，然後打開一個暫存檔。我們使用<code>"."</code>當作我們的暫存資料夾，因為<code>.</code>在幾乎任何作業系統中都代表了現在所在的資料夾。我們使用<code>"temp"</code>當作我們暫存檔的樣板名，他代表站存檔的名字會是temp接上某串隨機字串。他回傳一個創建暫存檔的I/O action，然後那個I/O action的結果是一個pair：暫存檔的名字跟一個handle。我們當然可以隨便開啟一個todo2.txt這種名字的檔案。但使用<code>openTempFile</code>會是比較好的作法，這樣你不會不小心覆寫任何檔案。</p><p>我們不用<code>getCurrentDirectory</code>的來拿到現在所在資料夾而用<code>"."</code>的原因是<code>.</code>在unix-like系統跟Windows中都表示現在的資料夾。</p><p>然後，我們綁定todo.txt的內容成<code>contents</code>。把字串斷成一串字串，每個字串代表一行。<code>todoTasks</code>就變成<code>["Iron the dishes", "Dust the dog", "Take salad out of the oven"]</code>。我們用一個會把3跟<code>"hey"</code>變成<code>"3 - hey"</code>的函數，然後從0開始把這個串列zip起來。所以<code>numberedTasks</code>就是<code>["0 - Iron the dishes", "1 - Dust the dog" ...</code>。我們用<code>unlines</code>把這個串列變成一行，然後印到終端上。注意我們也有另一種作法，就是用<code>mapM putStrLn numberedTasks</code>。</p><p>我們問使用者他們想要刪除哪一個並且等著他們輸入一個數字。假設他們想要刪除1號，那代表<code>Dust the dog</code>，所以他們輸入<code>1</code>。於是<code>numberString</code>就代表<code>"1"</code>。由於我們想要一個數字，而不是一個字串，所以我們用對<code>1</code>使用<code>read</code>，並且綁定到<code>number</code>。</p><p>還記得在<code>Data.List</code>中的<code>delete</code>跟<code>!!</code>嗎？<code>!!</code>回傳某個index的元素，而<code>delete</code>刪除在串列中第一個發現的元素，然後回傳一個新的沒有那個元素的串列。<code>(todoTasks !! number)</code>(number代表<code>1</code>) 回傳<code>"Dust the dog"</code>。我們把<code>todoTasks</code>去掉第一個<code>"Dust the dog"</code>後的串列綁定到<code>newTodoItems</code>，然後用<code>unlines</code>變成一行然後寫到我們所打開的暫存檔。舊有的檔案並沒有變動，而暫存檔包含砍掉那一行後的所有內容。</p><p>在我們關掉原始檔跟暫存檔之後我們用<strong>removeFile</strong>來移除原本的檔案。他接受一個檔案路徑並且刪除檔案。刪除舊得todo.txt之後，我們用<strong>renameFile</strong>來將暫存檔重新命名成todo.txt。特別留意<code>removeFile</code>跟<code>renameFile</code>(兩個都在<code>System.Directory</code>中)接受的是檔案路徑，而不是handle</p><p>這就是我們要得，實際上我們可以用更少行寫出同樣的程式，但我們很小心地避免覆寫任何檔案，並詢問作業系統我們可以把暫存檔擺在哪？讓我們來執行看看。</p><pre class="code">$ runhaskell deletetodo.hs  
These are your TO-DO items:  
0 - Iron the dishes  
1 - Dust the dog  
2 - Take salad out of the oven  
Which one do you want to delete?  
1  

$ cat todo.txt  
Iron the dishes  
Take salad out of the oven  

$ runhaskell deletetodo.hs  
These are your TO-DO items:  
0 - Iron the dishes  
1 - Take salad out of the oven  
Which one do you want to delete?  
0  
  
$ cat todo.txt  
Take salad out of the oven</pre><a name="命令列引數"></a><h2>命令列引數</h2><img src="img/arguments.png" style="float:right"></img><p>如果你想要寫一個在終端裡運行的程式，處裡命令列引數是不可或缺的。幸運地，利用Haskell的Standard Libary能讓我們有效地處裡命令列引數。</p><p>在之前的章節中，我們寫了一個能將to-do item加進或移除to-do list的一個程式。但我們的寫法有兩個問題。第一個是我們把放to-do list的檔案名稱給寫死了。我們擅自決定使用者不會有很多個to-do lists，就把檔案命名為todo.txt。</p><p>一種解決的方法是每次都詢問使用者他們想將他們的to-do list放進哪個檔案。我們在使用者要刪除的時候也採用這種方式。這是一種可以運作的方式，但不太能被接受，因為他需要使用者運行程式，等待程式詢問才能回答。這被稱為互動式的程式，但討厭的地方在當你想要自動化執行程式的時候，好比說寫成script，這會讓你的script寫起來比較困難。</p><p>這也是為什麼有時候使用者在執行的時候就告訴程式他們要什麼會比叫好，而不是讓程式去問使用者要什麼。更好的是是讓使用者透過命令列引數告訴程式他想要什麼。</p><p>在<code>System.Environment</code>模組當中有兩個很酷的I/O actions，一個是<strong>getArgs</strong>，他的type是<code>getArgs :: IO [String]</code>，他是一個拿取命令列引數的I/O action，並把結果放在包含的一個串列中。<strong>getProgName</strong>的型態是<code>getProgName :: IO String</code>，他則是一個I/O action包含了程式的名稱。</p><p>我們來看一個展現他們功能的程式。</p><pre class="code">import System.Environment   
import Data.List  
  
main = do  
    args <- getArgs  
    progName <- getProgName  
    putStrLn "The arguments are:"  
    mapM putStrLn args  
    putStrLn "The program name is:"  
    putStrLn progName</pre><p>我們將<code>getArgs</code>跟<code>progName</code>分別綁定到<code>args</code>跟<code>progName</code>。我們印出<code>The arguments are:</code>以及在<code>args</code>中的每個引數。最後，我們印出程式的名字。我們把程式編譯成<code>arg-test</code>。</p><pre class="code">$ ./arg-test first second w00t "multi word arg"  
The arguments are:  
first  
second  
w00t  
multi word arg  
The program name is:  
arg-test</pre><p>知道了這些函數現在你能寫幾個很酷的命令列程式。在之前的章節，我們寫了一個程式來加入待作事項，也寫了另一個程式刪除事項。現在我們要把兩個程式合起來，他會根據命令列引數來決定該做的事情。我們也會讓程式可以處裡不同的檔案，而不是只有todo.txt</p><p>我們叫這程式todo，他會作三件事：</p><ul><li><p>檢視待作事項</p></li><li><p>加入待作事項</p></li><li><p>刪除待作事項</p></li></ul><p>我們暫不考慮不合法的輸入這件事。</p><p>我們的程式要像這樣運作：假如我們要加入<code>Find the magic sword of power</code>，則我們會打<code>todo add todo.txt "Find the magic sword of power"</code>。要檢視事項我們則會打<code>todo view todo.txt</code>，如果要移除事項二則會打<code>todo remove todo.txt 2</code></p><p>我們先作一個分發的association list。他會把命令列引數當作key，而對應的處裡函數當作value。這些函數的型態都是<code>[String] -> IO ()</code>。他們會接受命令列引數的串列並回傳對應的檢視，加入以及刪除的I/O action。</p><pre class="code">import System.Environment   
import System.Directory  
import System.IO  
import Data.List  
  
dispatch :: [(String, [String] -> IO ())]  
dispatch =  [ ("add", add)  
            , ("view", view)  
            , ("remove", remove)  
            ]</pre><p>我們定義了<code>main</code>，<code>add</code>，<code>view</code>跟<code>remove</code>，</p><p>就從<code>main</code>開始講吧</p><pre class="code">main = do  
    (command:args) <- getArgs  
    let (Just action) = lookup command dispatch  
    action args  V</pre><p>首先，我們取出引數並把他們綁定到<code>(command:args)</code>。如果你還記得pattern matching，這麼做會把第一個引數綁定到<code>command</code>，把其他的綁定到<code>args</code>。如果我們像這樣執行程式<code>todo add todo.txt "Spank the monkey"</code>，<code>command</code>會變成<code>"add"</code>，而<code>args</code>會變成<code>["todo.xt", "Spank the monkey"]</code>。</p><p>在下一行，我們在一個分派的的串列中尋到我們的指令是哪個。由於<code>"add"</code>指向<code>add</code>，我們的結果便是<code>Just add</code>。我們再度使用了pattern matching來把我們的函數從<code>Maybe</code>中取出。但如果我們想要的指令不在分派的串列中呢？那樣lookup就會回傳<code>Nothing</code>，但我們這邊並不特別處裡失敗的情況，所以pattern matching會失敗然後我們的程式就會當掉。</p><p>最後，我們用剩下的引數呼叫<code>action</code>這個函數。他會還傳一個加入item，顯示所有items或者刪除item的I/O action。由於這個I/O action是在<code>main</code>的do block中，他最後會被執行。如果我們的<code>action</code>函數是<code>add</code>，他就會被餵<code>args</code>然後回傳一個加入<code>Spank the monkey</code>到todo.txt中的I/O action。</p><p>我們剩下要做的就是實做<code>add</code>，<code>view</code>跟<code>remove</code>，我們從<code>add</code>開始：</p><pre class="code">add :: [String] -> IO ()  
add [fileName, todoItem] = appendFile fileName (todoItem ++ "\n")</pre><p>如果我們這樣執行程式<code>todo add todo.txt "Spank the monkey"</code>，則<code>"add"</code>會被綁定到<code>command</code>，而<code>["todo.txt", "Spank the monkey"]</code>會被帶到從dispatch list中拿到的函數。</p><p>由於我們不處裡不合法的輸入，我們只針對這兩項作pattern matching，然後回傳一個附加一行到檔案末尾的I/O action。</p><p>接著，我們來實做檢視串列。如果我們想要檢視所有items，我們會<code>todo view todo.txt</code>。所以<code>command</code>會是<code>"view"</code>，而<code>args</code>會是<code>["todo.txt"]</code>。</p><pre class="code">view :: [String] -> IO ()  
view [fileName] = do  
    contents <- readFile fileName  
    let todoTasks = lines contents  
    numberedTasks = zipWith (\n line -> show n ++ " - " ++ line) [0..] todoTasks  
    putStr $ unlines numberedTasks</pre><p>這跟我們之前刪除檔案的程式差不多，只是我們是在顯示內容而已，</p><p>最後，我們要來實做<code>remove</code>。他基本上跟之前寫的只有刪除功能的程式很像，所以如果你不知道刪除是怎麼做的，可以去看之前的解釋。主要的差別是我們不寫死todo.txt，而是從參數取得。我們也不會提示使用者要刪除哪一號的item，而是從參數取得。</p><pre class="code">remove :: [String] -> IO ()  
remove [fileName, numberString] = do  
    handle <- openFile fileName ReadMode  
    (tempName, tempHandle) <- openTempFile "." "temp"  
    contents <- hGetContents handle  
    let number = read numberString  
        todoTasks = lines contents  
        newTodoItems = delete (todoTasks !! number) todoTasks  
    hPutStr tempHandle $ unlines newTodoItems  
    hClose handle  
    hClose tempHandle  
    removeFile fileName  
    renameFile tempName fileName</pre><p>我們打開<code>fileName</code>的檔案以及一個暫存。刪除使用者要我們刪的那一行後，把檔案內容寫到暫存檔。砍掉原本的檔案然後把暫存檔重新命名成<code>fileName</code>。</p><p>來看看完整的程式。</p><pre class="code">import System.Environment   
import System.Directory  
import System.IO  
import Data.List  
  
dispatch :: [(String, [String] -> IO ())]  
dispatch =  [ ("add", add)  
            , ("view", view)  
            , ("remove", remove)  
            ]  
   
main = do  
    (command:args) <- getArgs  
    let (Just action) = lookup command dispatch  
    action args  
  
add :: [String] -> IO ()  
add [fileName, todoItem] = appendFile fileName (todoItem ++ "\n")  
  
view :: [String] -> IO ()  
view [fileName] = do  
    contents <- readFile fileName  
    let todoTasks = lines contents  
        numberedTasks = zipWith (\n line -> show n ++ " - " ++ line) [0..] todoTasks  
    putStr $ unlines numberedTasks  
  
remove :: [String] -> IO ()  
remove [fileName, numberString] = do  
    handle <- openFile fileName ReadMode  
    (tempName, tempHandle) <- openTempFile "." "temp"  
    contents <- hGetContents handle  
    let number = read numberString  
        todoTasks = lines contents  
        newTodoItems = delete (todoTasks !! number) todoTasks  
    hPutStr tempHandle $ unlines newTodoItems  
    hClose handle  
    hClose tempHandle  
    removeFile fileName  
    renameFile tempName fileName</pre><img src="img/salad.png" style="float:left"></img><p>總結我們的程式：我們做了一個dispatch association，將指令對應到一些會接受命令列引數並回傳I/O action的函數。我們知道使用者下了什麼命令，並根據那個命令從dispatch list取出對影的函數。我們用剩下的命令列引數呼叫哪些函數而得到一些作相對應事情的I/O action。然後便執行那些I/O action。</p><p>在其他程式語言，我們可能會用一個大的switch case來實做，但使用高階函數讓我們可以要dispatch list給我們要的函數，並要那些函數給我們適當的I/O action。</p><p>讓我們看看執行結果。</p><pre class="code">$ ./todo view todo.txt  
0 - Iron the dishes  
1 - Dust the dog  
2 - Take salad out of the oven  

$ ./todo add todo.txt "Pick up children from drycleaners"  

$ ./todo view todo.txt  
0 - Iron the dishes  
1 - Dust the dog  
2 - Take salad out of the oven  
3 - Pick up children from drycleaners  

$ ./todo remove todo.txt 2  

$ ./todo view todo.txt  
0 - Iron the dishes  
1 - Dust the dog  
2 - Pick up children from drycleaners</pre><p>要再另外加新的選項也是很容易。只要在dispatch list加入新的會作你要的事情函數。你可以試試實做一個<code>bump</code>函數，接受一個檔案跟一個task number，他會回傳一個把那個task搬到to-do list頂端的I/O action。</p><p>對於不合法的輸入你也可以讓程式結束地漂亮一點。(例如使用者輸入了<code>todo UP YOURS HAHAHAHA</code>)可以作一個回報錯誤的I/O action (例如errorExist :: IO ())檢查有沒有不合法的輸入，如果有便執行這個回報錯誤的I/O action。我們之後會談另一個可能，就是用exception。</p><a name="亂數"></a><h2>亂數</h2><img src="img/random.png" style="float:right"></img><p>在許多情況下，你寫程式會需要些隨機的資料。或許你在製作一個遊戲，在遊戲中你需要擲骰子。或是你需要測試程式的測試資料。精準一點地說，我們需要pseudo-random的資料，我們知道真正的隨機資料好比是一隻猴子拿著起司跟奶油騎在單輪車上，任何事情都會發生。在這個章節，我們要看看如何讓Haskell產生些pseudo-random的資料。</p><p>在大多數其他的程式語言中，會給你一些函數能讓你拿到些隨機亂數。每呼叫一次他就會拿到一個不同的數字。那在Haskell中是如何？要記住Haskell是一個純粹函數式語言。代表任何東西都具有referential transparency。那代表你餵給一個函數相同的參數，不管怎麼呼叫都是回傳相同的結果。這很新奇的原因是因為他讓我們理解程式的方式不同，而且可以讓我們延遲計算，直到我們真正需要他。如果我呼叫一個函數，我可以確定他不會亂來。我真正在乎的是他的結果。然而，這會造成在亂數的情況有點複雜。如果我有一個函數像這樣：</p><pre class="code">randomNumber :: (Num a) => a
randomNumber = 4</pre><p>由於他永遠回傳<code>4</code>，所以對於亂數的情形而言是沒什麼意義。就算4這個結果是擲骰子來的也沒有意義。</p><p>其他的程式語言是怎麼產生亂數的呢？他們可能隨便拿取一些電腦的資訊，像是現在的時間，你怎麼移動你的滑鼠，以及周圍的聲音。根據這些算出一個數值讓他看起來好像隨機的。那些要素算出來的結果可能在每個時間都不同，所以你會拿到不同的隨機數字。</p><p>所以說在Haskell中，假如我們能作一個函數，他會接受一個具隨機性的參數，然後根據那些資訊還傳一個數值。</p><p>在<code>System.Random</code>模組中。他包含所有滿足我們需求的函數。讓我們先來看其中一個，就是<strong>random</strong>。他的型態是<code>random :: (RandomGen g, Random a) => g -> (a, g)</code>。哇，出現了新的typeclass。<strong>RandomGen</strong>typeclass是指那些可以當作亂源的型態。而<strong>Random</strong>typeclass則是可以裝亂數的型態。一個布林值可以是隨機值，不是<code>True</code>就是<code>False</code>。一個整數可以是隨機的好多不同值。那你會問，函數可以是一個隨機值嗎？我不這麼認為。如果我們試著翻譯<code>random</code>的型態宣告，大概會是這樣：他接受一個random generator (亂源所在)，然後回傳一個隨機值以及一個新的random generator。為什麼他要回傳一個新的random generator呢？就是下面我們要講的。</p><p>要使用<code>random</code>函數， 我們必須要了解random generator。 在<code>System.Random</code>中有一個很酷的型態，叫做<strong>StdGen</strong>， 他是<code>RandomGen</code>的一個instance。 我們可以自己手動作一個<code>StdGen</code>也可以告訴系統給我們一個現成的。</p><p>要自己做一個random generator，要使用<strong>mkStdGen</strong>這個函數。他的型態是<code>mkStdGen :: Int -> StdGen</code>。他接受一個整數，然後根據這個整數會給一個random generator。讓我們來試一下<code>random</code>以及<code>mkStdGen</code>，用他們產生一個亂數吧。</p><pre class="code">ghci> random (mkStdGen 100)</pre><pre class="code"><interactive>:1:0:  
    Ambiguous type variable `a' in the constraint:  
        `Random a' arising from a use of `random' at <interactive>:1:0-20  
    Probable fix: add a type signature that fixes these type variable(s)  `</pre><p>這是什麼？由於<code>random</code>函數會回傳<code>Random</code>typeclass中任何一種型態，所以我們必須告訴Haskell我們是要哪一種型態。不要忘了我們是回傳random value跟random generator的一個pair</p><pre class="code">ghci> random (mkStdGen 100) :: (Int, StdGen)  
(-1352021624,651872571 1655838864)</pre><p>我們終於有了一個看起來像亂數的數字。tuple的第一個部份是我們的亂數，而第二個部份是一個新的random generator的文字表示。如果我們用相同的random generator再呼叫<code>random</code>一遍呢？</p><pre class="code">ghci> random (mkStdGen 100) :: (Int, StdGen)  
(-1352021624,651872571 1655838864)</pre><p>不易外地我們得到相同的結果。所以我們試試用不同的random generator作為我們的參數。</p><pre class="code">ghci> random (mkStdGen 949494) :: (Int, StdGen)  
(539963926,466647808 1655838864)</pre><p>很好，我們拿到了不同的數字。我們可以用不同的型態標誌來拿到不同型態的亂數</p><pre class="code">ghci> random (mkStdGen 949488) :: (Float, StdGen)  
(0.8938442,1597344447 1655838864)  
ghci> random (mkStdGen 949488) :: (Bool, StdGen)  
(False,1485632275 40692)  
ghci> random (mkStdGen 949488) :: (Integer, StdGen)  
(1691547873,1597344447 1655838864)</pre><p>讓我們寫一個模擬丟三次銅板的函數。假如<code>random</code>不同時回傳一個亂數以及一個新的random generator，我們就必須讓這函數接受三個random generators讓他們每個回傳一個擲銅板的結果。但那樣聽起來怪怪的，加入一個generator可以產生一個型態是<code>Int</code>的亂數，他應該可以產生擲三次銅板的結果（總共才八個組合）。這就是<code>random</code>為什麼要回傳一個新的generator的關鍵了。</p><p>我們將一個銅板表示成<code>Bool</code>。<code>True</code>代表反面，<code>False</code>代表正面。</p><pre class="code">threeCoins :: StdGen -> (Bool, Bool, Bool)  
threeCoins gen =   
    let (firstCoin, newGen) = random gen  
    (secondCoin, newGen') = random newGen  
    (thirdCoin, newGen') = random newGen'  
    in  (firstCoin, secondCoin, thirdCoin)  )</pre><p>我們用我們拿來當參數的generator呼叫<code>random</code>並得到一個擲銅板的結果跟一個新的generator。然後我們再用新的generator呼叫他一遍，來得到第二個擲銅板的結果。對於第三個擲銅板的結果也是如法炮製。如果我們一直都用同樣的generator，那所有的結果都會是相同的值。也就是不是<code>(False, False, False)</code>就是<code>(True, True, True)</code>。</p><pre class="code">ghci> threeCoins (mkStdGen 21)  
(True,True,True)  
ghci> threeCoins (mkStdGen 22)  
(True,False,True)  
ghci> threeCoins (mkStdGen 943)  
(True,False,True)  
ghci> threeCoins (mkStdGen 944)  
(True,True,True)</pre><p>留意我們不需要寫<code>random gen :: (Bool, StdGen)</code>。那是因為我們已經在函數的型態宣告那邊就表明我們要的是布林。而Haskell可以推敲出我們要的是布林值。</p><p>假如我們要的是擲四次？甚至五次呢？有一個函數叫<strong>randoms</strong>，他接受一個generator並回傳一個無窮序列。</p><pre class="code">ghci> take 5 $ randoms (mkStdGen 11) :: [Int]  
[-1807975507,545074951,-1015194702,-1622477312,-502893664]  
ghci> take 5 $ randoms (mkStdGen 11) :: [Bool]  
[True,True,True,True,False]  
ghci> take 5 $ randoms (mkStdGen 11) :: [Float]  
[7.904789e-2,0.62691015,0.26363158,0.12223756,0.38291094]</pre><p>為什麼<code>randoms</code>不另外多回傳一個新的generator呢？我們可以這樣地實做<code>randoms</code></p><pre class="code">randoms' :: (RandomGen g, Random a) => g -> [a]  
randoms' gen = let (value, newGen) = random gen in value:randoms' newGen</pre><p>一個遞迴的定義。我們由現在的generator拿到一個亂數跟一個新的generator，然後製作一個list，list的第一個值是那個亂數，而list的其餘部份是根據新的generator產生出的其餘亂數們。由於我們可能產生出無限的亂數，所以不可能回傳一個新的generator。</p><p>我們可以寫一個函數，他會回傳有限個亂數跟一個新的generator</p><pre class="code">finiteRandoms :: (RandomGen g, Random a, Num n) => n -> g -> ([a], g)  
finiteRandoms 0 gen = ([], gen)  
finiteRandoms n gen =   
    let (value, newGen) = random gen  
        (restOfList, finalGen) = finiteRandoms (n-1) newGen  
    in  (value:restOfList, finalGen)</pre><p>又是一個遞迴的定義。我們說如果我們要0個亂數，我們便回傳一個空的list跟原本給我們的generator。對於其他數量的亂數，我們先拿一個亂數跟一個新的generator。這一個亂數便是list的第一個數字。然後list中剩下的便是n-1個由新的generator產生出的亂數。然後我們回傳整個list跟最後一個產生完n-1個亂數後generator。</p><p>如果我們要的是在某個範圍內的亂數呢？現在拿到的亂數要不是太大就是太小。如果我們想要的是骰子上的數字呢？<strong>randomR</strong>能滿足我們的需求。他的型態是<code>randomR :: (RandomGen g, Random a) :: (a, a) -> g -> (a, g)</code>，代表他有點類似<code>random</code>。只不過他的第一個參數是一對數目，定義了最後產生亂數的上界以及下界。</p><pre class="code">ghci> randomR (1,6) (mkStdGen 359353)  
(6,1494289578 40692)  
ghci> randomR (1,6) (mkStdGen 35935335)  
(3,1250031057 40692)</pre><p>另外也有一個<strong>randomRs</strong>的函數，他會產生一連串在給定範圍內的亂數：</p><pre class="code">ghci> take 10 $ randomRs ('a','z') (mkStdGen 3) :: [Char]  
"ndkxbvmomg"</pre><p>這結果看起來像是一個安全性很好的密碼。</p><p>你會問你自己，這一單元跟I/O有關系嗎？到現在為止還沒出現任何跟I/O有關的東西。到現在為止我們都是手動地做我們的random generator。但那樣的問題是，程式永遠都會回傳同樣的亂數。這在真實世界中的程式是不能接受的。這也是為什麼<code>System.Random</code>要提供<strong>getStdGen</strong>這個I/O action，他的型態是<code>IO StdGen</code>。當你的程式執行時，他會跟系統要一個random generator，並存成一個global generator。<code>getStdGen</code>會替你拿那個global random generator並把他綁定到某個名稱上。</p><p>這裡有一個簡單的產生隨機字串的程式。</p><pre class="code">import System.Random  
  
main = do  
    gen <- getStdGen  
    putStr $ take 20 (randomRs ('a','z') gen)</pre><pre class="code">$ runhaskell random_string.hs  
pybphhzzhuepknbykxhe  
$ runhaskell random_string.hs  
eiqgcxykivpudlsvvjpg  
$ runhaskell random_string.hs  
nzdceoconysdgcyqjruo  
$ runhaskell random_string.hs  
bakzhnnuzrkgvesqplrx</pre><p>要當心當我們連續兩次呼叫<code>getStdGent</code>的時候，實際上都會回傳同樣的global generator。像這樣：</p><pre class="code">import System.Random  
  
main = do  
    gen <- getStdGen  
    putStrLn $ take 20 (randomRs ('a','z') gen)  
    gen2 <- getStdGen  
    putStr $ take 20 (randomRs ('a','z') gen2)</pre><p>你會印出兩次同樣的字串。要能得到兩個不同的字串是建立一個無限的stream，然後拿前20個字當作第一個字串，拿下20個字當作第二個字串。要這麼做，我們需要在<code>Data.List</code>中的<code>splitAt</code>函數。他會把一個list根據給定的index切成一個tuple，tuple的第一部份就是切斷的前半，第二個部份就是切斷的後半。</p><pre class="code">import System.Random  
import Data.List  
  
main = do  
    gen <- getStdGen  
    let randomChars = randomRs ('a','z') gen  
        (first20, rest) = splitAt 20 randomChars  
        (second20, _) = splitAt 20 rest  
    putStrLn first20  
    putStr second20</pre><p>另一種方法是用<strong>newStdGen</strong>這個I/O action，他會把現有的random generator分成兩個新的generators。然後會把其中一個指定成global generator，並回傳另一個。</p><pre class="code">import System.Random  
  
main = do     
    gen <- getStdGen     
    putStrLn $ take 20 (randomRs ('a','z') gen)     
    gen' <- newStdGen  
    putStr $ take 20 (randomRs ('a','z') gen')</pre><p>當我們綁定<code>newStdGen</code>的時候我們不只是會拿到一個新的generator，global generator也會被重新指定。所以再呼叫一次<code>getStdGen</code>並綁定到某個名稱的話，我們就會拿到跟<code>gen</code>不一樣的generator。</p><p>這邊有一個小程式會讓使用者猜數字：</p><pre class="code">import System.Random  
import Control.Monad(when)  
  
main = do  
    gen <- getStdGen  
    askForNumber gen  

askForNumber :: StdGen -> IO ()  
askForNumber gen = do  
    let (randNumber, newGen) = randomR (1,10) gen :: (Int, StdGen)  
    putStr "Which number in the range from 1 to 10 am I thinking of? "  
    numberString <- getLine  
    when (not $ null numberString) $ do  
        let number = read numberString  
        if randNumber == number   
            then putStrLn "You are correct!"  
            else putStrLn $ "Sorry, it was " ++ show randNumber  
            askForNumber newGen</pre><img src="img/jackofdiamonds.png" style="float:left"></img><p>我們寫了一個<code>askForNumber</code>的函數，他接受一個random generator並回傳一個問使用者要數字並回答是否正確的I/O action。在那個函數裡面，我們先根據從參數拿到的generator產生一個亂數以及一個新的generator，分別叫他們為<code>randomNumber</code>跟<code>newGen</code>。假設那個產生的數字是<code>7</code>。則我們要求使用者猜我們握有的數字是什麼。我們用<code>getLine</code>來將結果綁定到<code>numberString</code>上。當使用者輸入<code>7</code>，<code>numberString</code>就會是<code>"7"</code>。接下來，我們用<code>when</code>來檢查使用者輸入的是否是空字串。如果是，那一個空的I/O action<code>return ()</code>就會被回傳。基本上就等於是結束程式的意思。如果不是，那I/O action就會被執行。我們用<code>read</code>來把<code>numberString</code>轉成一個數字，所以<code>number</code>便會是<code>7</code>。</p><blockquote><p>如果使用者給我們一些<code>read</code>沒辦法讀取的輸入（像是<code>"haha"</code>），我們的程式便會當掉並印出錯誤訊息。 如果你不希望你的程式當掉，就用<strong>reads</strong>，當讀取失敗的時候他會回傳一個空的list。當成功的時候他就回傳一個 tuple，第一個部份是我們想要的數字，第二個部份是讀取失敗的字串。</p></blockquote><p>我們檢查如果輸入的數字跟我們隨機產生的數字一樣，便提示使用者恰當的訊息。然後再遞迴地呼叫<code>askForNumber</code>，只是會拿到一個新的generator。就像之前的generator一樣，他會給我們一個新的I/O action。</p><p><code>main</code>的組成很簡單，就是由拿取一個random generator跟呼叫<code>askForNumber</code>組成罷了。</p><p>來看看我們的程式：</p><pre class="code">$ runhaskell guess_the_number.hs  
Which number in the range from 1 to 10 am I thinking of? 4  
Sorry, it was 3  
Which number in the range from 1 to 10 am I thinking of? 10  
You are correct!  
Which number in the range from 1 to 10 am I thinking of? 2  
Sorry, it was 4  
Which number in the range from 1 to 10 am I thinking of? 5  
Sorry, it was 10  
Which number in the range from 1 to 10 am I thinking of?</pre><p>用另一種方式寫的話像這樣：</p><pre class="code">import System.Random  
import Control.Monad(when)  
  
main = do  
    gen <- getStdGen  
    let (randNumber, _) = randomR (1,10) gen :: (Int, StdGen)     
    putStr "Which number in the range from 1 to 10 am I thinking of? "  
    numberString <- getLine  
    when (not $ null numberString) $ do  
        let number = read numberString  
        if randNumber == number  
            then putStrLn "You are correct!"  
            else putStrLn $ "Sorry, it was " ++ show randNumber  
        newStdGen  
        main</pre><p>他非常類似我們之前的版本，只是不是遞迴地呼叫，而是把所有的工作都在<code>main</code>裡面做掉。在告訴使用者他們猜得是否正確之後，便更新global generator然後再一次呼叫<code>main</code>。兩種策略都是有效但我比較喜歡第一種方式。因為他在<code>main</code>裡面做的事比較少，並提供我們一個可以重複使用的函數。</p><a name="Bytestrings"></a><h2>Bytestrings</h2><img src="img/chainchomp.png" style="float:right"></img><p>List是一種有用又酷的資料結構。到目前為止，我們幾乎無處不使用他。有好幾個函數是專門處裡List的，而Haskell惰性的性質又讓我們可以用filter跟map來替換其他語言中的for loop跟while loop。也由於evaluation只會發生在需要的時候，像infinite list也對於Haskell不成問題（甚至是infinite list of infinite list）。這也是為什麼list能被用來表達stream，像是讀取標準輸入或是讀取檔案。我們可以打開檔案然後讀取內容成字串，即便實際上我們是需要的時候才會真正取讀取。</p><p>然而，用字串來處裡檔案有一個缺點：就是他很慢。想你所知道的，<code>String</code>是一個<code>[Char]</code>的type synonym。<code>Char</code>沒有一個固定的大小，因為他可能由好幾個byte組成，好比說Unicode。再加上list是惰性的。如果你有一個list像<code>[1,2,3,4]</code>，他只會在需要的時候被evaluate。所以整個list其實比較像是一個"保證"你會有一個list。要記住<code>[1,2,3,4]</code>不過是<code>1:2:3:4:[]</code>的一個syntactic sugar。當list的第一個元素被evaluated的時候，剩餘的部份<code>2:3:4:[]</code>一樣也只是一個"保證"你會有一個list，以此類推。以此類推。以此類推。所以你可以想像成list是保證在你需要的時候會給你第一個元素，以及保證你會有剩下的部份當你還需要更多的時候。其實不難說服你這樣做並不是一個最有效率的作法。</p><p>這樣額外的負擔在大多數時候不會造成困擾，但當我們要讀取一個很大的檔案的時候就是個問題了。這也是為什麼Haskell要有<code>bytestrings</code>。Bytestrings有點像list，但他每一個元素都是一個byte (8 bits)，而且他們惰性的程度也是不同。</p><p>Bytesstrings有兩種：strict跟lazy。Strict bytestrings放在<code>Data.ByteString</code>，他們把惰性的性質完全拿掉。不會有所謂任何的"保證"，一個strict bytestring就代表一連串的bytes。因此你不會有一個無限長的strict bytestrings。如果你evaluate第一個byte，你就必須evalute整個bytestring。這麼做的優點是他會比較少overhaed，因為他沒有"Thunk"(也就是用Haskell術語來說的"保證")。缺點就是他可能會快速消耗你的記憶體，因為你把他們一次都讀進了記憶體。</p><p>另一種bytestring是放在<code>Data.ByteString.Lazy</code>中。他們具有惰性，但又不像list那麼極端。就像我們之前說的，List的thunk個數是跟list中有幾個元素一模一樣。這也是為什麼他們速度沒辦法滿足一些特殊需求。Lazy bytestrings則用另一種作法，他們被存在chunks中(不要跟Thunk搞混)，每一個chunck的大小是64K。所以如果你evaluate lazy bytestring中的byte，則前64K會被evaluated。在那個chunck之後，就是一些"保證"會有剩餘的chunk。lazy bytestrings有點像裝了一堆大小為64K的strict bytestrings的list。當你用lazy bytestring處裡一個檔案的時候，他是一個chunk一個chunk去讀。這很棒是因為他不會讓我們一下使用大量的記憶體，而且64K有很高的可能性能夠裝進你CPU的L2 Cache。</p><p>如果你大概看過<code>Data.ByteString.Lazy</code>的文件，你會看到到他有一堆函數的名稱跟<code>Data.List</code>中的函數名稱相同，只是出現的type signature是<code>ByteString</code>而不是<code>[a]</code>，是<code>Word8</code>而不是<code>a</code>。同樣名稱的函數基本上表現的行為跟list中的差不多。因為名稱是一樣的，所以必須用qualified import才不會在裝載進GHCI的時候造成衝突。</p><pre class="code">import qualified Data.ByteString.Lazy as B
import qualified Data.ByteString as S</pre><p><code>B</code>中有lazy bytestrings跟對應的函數，而<code>S</code>中則有strict的版本。大多數時候我們是用lazy的版本。</p><p><strong>pack</strong>函數的type signature是<code>pack :: [Word8] -> ByteString</code>。代表他接受一串型態為<code>Word8</code>的bytes，並回傳一個<code>ByteString</code>。你能想像一個lazy的list，要讓他稍微不lazy一些，所以讓他對於64K lazy。</p><p>那<code>Word8</code>型態又是怎麼一回事？。他就像<code>Int</code>，只是他的範圍比較小，介於0-255之間。他代表一個8-bit的數字。就像<code>Int</code>一樣，他是屬於<code>Num</code>這個typeclass。例如我們知道<code>5</code>是polymorphic的，他能夠表現成任何數值型態。其實<code>Word8</code>他也能表示。</p><pre class="code">ghci> B.pack [99,97,110]  
Chunk "can" Empty  
ghci> B.pack [98..120]  
Chunk "bcdefghijklmnopqrstuvwx" Empty</pre><p>正如你看到的，你其實不必特別在意<code>Word8</code>，因為型態系統會選擇正確的型態。如果你試著用比較大的數字，像是<code>336</code>。那對於<code>Word8</code>他就會變成<code>80</code>。</p><p>我們把一些數值打包成<code>ByteString</code>，使他們可以塞進一個chunk裡面。<code>Empty</code>之於<code>ByteString</code>就像<code>[]</code>之於list一樣。</p><p><strong>unpack</strong>是<code>pack</code>的相反，他把一個bytestring變成一個byte list。</p><p><strong>fromChunks</strong>接受一串strict的bytestrings並把他變成一串lazy bytestring。<strong>toChunks</strong>接受一個lazy bytestrings並將他變成一串strict bytestrings。</p><pre class="code">ghci> B.fromChunks [S.pack [40,41,42], S.pack [43,44,45], S.pack [46,47,48]]  
Chunk "()*" (Chunk "+,-" (Chunk "./0" Empty))</pre><p>如果你有很多小的strict bytestrings而且不想先將他們join起來（會耗損memory）這樣的作法是不錯的。</p><p>bytestring版本的<code>:</code>叫做<strong>cons</strong>。他接受一個byte跟一個bytestring，並把這個byte放到bytestring的前端。他是lazy的操作，即使bytestring的第一個chunk不是滿的，他也會新增一個chunk。這也是為什麼當你要插入很多bytes的時候最好用strict版本的<code>cons</code>，也就是<strong>cons'</strong>。</p><pre class="code">ghci> B.cons 85 $ B.pack [80,81,82,84]  
Chunk "U" (Chunk "PQRT" Empty)  
ghci> B.cons' 85 $ B.pack [80,81,82,84]  
Chunk "UPQRT" Empty  
ghci> foldr B.cons B.empty [50..60]  
Chunk "2" (Chunk "3" (Chunk "4" (Chunk "5" (Chunk "6" (Chunk "7" (Chunk "8" (Chunk "9" (Chunk ":" (Chunk ";" (Chunk "<"  
Empty))))))))))  
ghci> foldr B.cons' B.empty [50..60]  
Chunk "23456789:;<" Empty</pre><p>你可以看到<strong>empty</strong>製造了一個空的bytestring。也注意到<code>cons</code>跟<code>cons'</code>的差異了嗎？有了<code>foldr</code>，我們逐步地把一串數字從右邊開始，一個個放到bytestring的前頭。當我們用<code>cons</code>，我們則得到一個byte一個chunk的結果，並不是我們要的。</p><p>bytestring模組有一大票很像<code>Data.List</code>中的函數。包括了<code>head</code>，<code>tail</code>，<code>init</code>，<code>null</code>，<code>length</code>，<code>map</code>，<code>reverse</code>，<code>foldl</code>，<code>foldr</code>，<code>concat</code>，<code>takeWhile</code>，<code>filter</code>，等等。</p><p>他也有表現得跟<code>System.IO</code>中一樣的函數，只有<code>Strings</code>被換成了<code>ByteString</code>而已。像是<code>System.IO</code>中的<code>readFile</code>，他的型態是<code>readFile :: FilePath -> IO String</code>，而bytestring模組中的<strong>readFile</strong>則是<code>readFile :: FilePath -> IO ByteString</code>。小心，如果你用了strict bytestring來讀取一個檔案，他會把檔案內容都讀進記憶體中。而使用lazy bytestring，他則會讀取chunks。</p><p>讓我們來寫一個簡單的程式，他從命令列接受兩個檔案名，然後拷貝第一個檔案內容成第二個檔案。雖然<code>System.Directory</code>中已經有一個函數叫<code>copyFile</code>，但我們想要實做自己的版本。</p><pre class="code">import System.Environment  
import qualified Data.ByteString.Lazy as B  
  
main = do  
    (fileName1:fileName2:_) <- getArgs  
    copyFile fileName1 fileName2  

copyFile :: FilePath -> FilePath -> IO ()  
copyFile source dest = do  
    contents <- B.readFile source  
    B.writeFile dest contents</pre><p>我們寫了自己的函數，他接受兩個<code>FilePath</code>（記住<code>FilePath</code>不過是<code>String</code>的同義詞。）並回傳一個I/O action，他會用bytestring拷貝第一個檔案至另一個。在<code>main</code>函數中，我們做的只是拿到命令列引數然後呼叫那個函數來拿到一個I/O action。</p><pre class="code">$ runhaskell bytestringcopy.hs something.txt ../../something.txt</pre><p>就算我們不用bytestring來寫，程式最後也會長得像這樣。差別在於我們會用<code>B.readFile</code>跟<code>B.writeFile</code>而不是<code>readFile</code>跟<code>writeFile</code>。有很大的可能性，就是你只要import檔案並在函數前加上qualified模組名，就可以把一個用正常String的程式改成用ByteString。也有可能你是要反過來做，但那也不難。</p><p>當你需要更好的效能來讀取許多資料，嘗試用bytestring，有很大的機會你會用很小的力氣改進很多效能。我通常用正常String來寫程式，然後在效能不好的時候把他們改成ByteString。</p><a name="Exceptions (例外)"></a><h2>Exceptions (例外)</h2><img src="img/timber.png" style="float:left"></img><p>所有的程式語言都有要處裡失敗的情形。這就是人生。不同的語言有不同的處裡方式。在C裡面，我們通常用非正常範圍的回傳值（像是<code>-1</code>或null）來回傳錯誤。Java跟C#則傾向於使用exception來處裡失敗的情況。當一個exception被丟出的時候，控制流程就會跳到我們做一些清理動作的地方，做完清理後exception被重新丟出，這樣一些處裡錯誤的程式碼可以完成他們的工作。</p><p>Haskell有一個很棒的型態系統。Algebraic data types允許像是<code>Maybe</code>或<code>Either</code>這種型態，我們能用這些型態來代表一些可能有或沒有的結果。在C裡面，在失敗的時候回傳<code>-1</code>是很常見的事。但他只對寫程式的人有意義。如果我們不小心，我們有可能把這些錯誤碼當作正常值來處裡，便造成一些諢亂。Haskell的型態系統賦予我們更安全的環境。一個<code>a -> Maybe b</code>的函數指出了他會產生一個包含<code>b</code>的<code>Just</code>，或是回傳<code>Nothing</code>。這型態跟<code>a -> b</code>是不同的，如果我們試著將兩個函數混用，compiler便會警告我們。</p><p>儘管有表達力夠強的型態來輔助失敗的情形，Haskell仍然支持exception，因為exception在I/O的contexts下是比較合理的。在處裡I/O的時候會有一堆奇奇怪怪的事情發生，環境是很不能被信賴的。像是打開檔案。檔案有可能被lock起來，也有可能檔案被移除了，或是整個硬碟都被拔掉。所以直接跳到處裡錯誤的程式碼是很合理的。</p><p>我們了解到I/O code會丟出exception是件合理的事。至於pure code呢？其實他也能丟出Exception。想想看<code>div</code>跟<code>head</code>兩個案例。他們的型態是<code>(Integral a) => a -> a -> a</code>以及<code>[a] -> a</code>。<code>Maybe</code>跟<code>Either</code>都沒有在他們的回傳型態中，但他們都有可能失敗。<code>div</code>有可能除以零，而<code>head</code>有可能你傳給他一個空的list。</p><pre class="code">ghci> 4 `div` 0  
*** Exception: divide by zero  
ghci> head []  
*** Exception: Prelude.head: empty list</pre><img src="img/police.png" style="float:left"></img><p>pure code能丟出Exception，但Exception只能在I/O section中被接到（也就是在<code>main</code>的do block中）這是因為在pure code中你不知道什麼東西什麼時候會被evaluate。因為lazy特性的緣故，程式沒有一個特定的執行順序，但I/O code有。</p><p>先前我們談過為什麼在I/O部份的程式要越少越好。程式的邏輯部份盡量都放在pure的部份，因為pure的特性就是他們的結果只會根據函數的參數不同而改變。當思考pure function的時候，你只需要考慮他回傳什麼，因為除此之外他不會有任何副作用。這會讓事情簡單許多。儘管I/O的部份是難以避免的（像是打開檔案之類），但最好是把I/O部份降到最低。Pure functions預設是lazy，那代表我們不知道他什麼時候會被evaluate，不過我們也不該知道。然而，一旦pure functions需要丟出Exception，他們何時被evaluate就很重要了。那是因為我們只有在I/O的部份才能接到Exception。這很糟糕，因為我們說過希望I/O的部份越少越好。但如果我們不接Exception，我們的程式就會當掉。這問題有解決辦法嗎？答案是不要在pure code裡面使用Exception。利用Haskell的型態系統，盡量使用<code>Either</code>或<code>Maybe</code>之類的型態來表示可能失敗的計算。</p><p>這也是為什麼我們要來看看怎麼使用I/O Excetion。I/O Exception是當我們在<code>main</code>裡面跟外界溝通失敗而丟出的Exception。例如我們嘗試打開一個檔案，結果發現他已經被刪掉或是其他狀況。來看看一個嘗試打開命令列引數所指定檔案名稱，並計算裡面有多少行的程式。</p><pre class="code">import System.Environment  
import System.IO  
  
main = do (fileName:_) <- getArgs  
            contents <- readFile fileName  
            putStrLn $ "The file has " ++ show (length (lines contents)) ++ " lines!"</pre><p>一個很簡單的程式。我們使用<code>getArgs</code>I/O action，並綁定第一個string到<code>fileName</code>。然後我們綁定檔案內容到<code>contents</code>。最後，我們用<code>lines</code>來取得line的list，並計算list的長度，並用<code>show</code>來轉換數字成string。他如我們想像的工作，但當我們給的檔案名稱不存在的時候呢？</p><pre class="code">$ runhaskell linecount.hs i_dont_exist.txt  
linecount.hs: i_dont_exist.txt: openFile: does not exist (No such file or directory)</pre><p>GHC丟了錯誤訊息給我們，告訴我們檔案不存在。然後程式就掛掉了。假如我們希望印出比較好一些的錯誤訊息呢？一種方式就是在打開檔案前檢查他存不存在。用<code>System.Directory</code>中的<strong>doesFileExist</strong>。</p><pre class="code">import System.Environment  
import System.IO  
import System.Directory  
  
main = do (fileName:_) <- getArgs  
            fileExists <- doesFileExist fileName  
            if fileExists  
                then do contents <- readFile fileName  
                    putStrLn $ "The file has " ++ show (length (lines contents)) ++ " lines!"  
                else do putStrLn "The file doesn't exist!"</pre><p>由於<code>doesFileExist</code>的型態是<code>doesFileExist :: FilePath -> IO Bool</code>，所以我們要寫成<code>fileExists <- doesFileExist fileName</code>。那代表他回傳含有一個布林值告訴我們檔案存不存在的I/O action。<code>doesFileExist</code>是不能直接在if expression中使用的。</p><p>另一個解法是使用Exception。在這個情境下使用Exception是沒問題的。檔案不存在這個Exception是在I/O中被丟出，所以在I/O中接起來也沒什麼不對。</p><p>要這樣使用Exception，我們必須使用<code>System.IO.Error</code>中的<strong>catch</strong>函數。他的型態是<code>catch :: IO a -> (IOError -> IO a) -> IO a</code>。他接受兩個參數，第一個是一個I/O action。像是他可以接受一個打開檔案的I/O action。第二個是handler。如果第一個參數的I/O action丟出了Exception，則他會被傳給handler，他會決定要作些什麼。所以整個I/O action的結果不是如預期中做完第一個參數的I/O action，就是handler處裡的結果。</p><img src="img/puppy.png" style="float:right"></img><p>如果你對其他語言像是Java, Python中try-catch的形式很熟，那<code>catch</code>其實跟他們很像。第一個參數就是其他語言中的try block。第二個參數就是其他語言中的catch block。其中handler只有在exception被丟出時才會被執行。</p><p>handler接受一個<code>IOError</code>型態的值，他代表的是一個I/O exception已經發生了。他也帶有一些exception本身的資訊。至於這型態在語言中使如何被實做則是要看編譯器。這代表我們沒辦法用pattern matching的方式來檢視<code>IOError</code>。就像我們不能用pattern matching來檢視<code>IO something</code>的內容。但我們能用一些predicate來檢視他們。</p><p>我們來看看一個展示<code>catch</code>的程式</p><pre class="code">import System.Environment  
import System.IO  
import System.IO.Error  
  
main = toTry `catch` handler  

toTry :: IO ()  
toTry = do (fileName:_) <- getArgs  
            contents <- readFile fileName  
            putStrLn $ "The file has " ++ show (length (lines contents)) ++ " lines!"  

handler :: IOError -> IO ()  
handler e = putStrLn "Whoops, had some trouble!"</pre><p>首先你看到我們可以在關鍵字周圍加上backticks來把<code>catch</code>當作infix function用，因為他剛好接受兩個參數。這樣使用讓可讀性變好。<code>toTry `catch` handler</code>跟<code>catch toTry handler</code>是一模一樣的。<code>toTry</code>是一個I/O action，而<code>handler</code>接受一個<code>IOError</code>，並回傳一個當exception發生時被執行的I/O action。</p><p>來看看執行的結果。</p><pre class="code">$ runhaskell count_lines.hs i_exist.txt  
The file has 3 lines!  
  
$ runhaskell count_lines.hs i_dont_exist.txt  
Whoops, had some trouble!</pre><p>在handler裡面我們並沒有檢查我們拿到的是什麼樣的<code>IOError</code>，我們只是印出<code>"Whoops, had some trouble!"</code>。接住任何種類的Exception就跟其他語言一樣，在Haskell中也不是一個好的習慣。假如其他種類的Exception發生了，好比說我們送一個中斷指令，而我們沒有接到的話會發生什麼事？這就是為什麼我們要做跟其他語言一樣的事：就是檢查我們拿到的是什麼樣的Exception。如果說是我們要的Exception，那就做對應的處裡。如果不是，我們在重新丟出Exception。我們把我們的程式這樣修改，只接住檔案不存在的Exception。</p><pre class="code">import System.Environment  
import System.IO  
import System.IO.Error  
  
main = toTry `catch` handler  

toTry :: IO ()  
toTry = do (fileName:_) <- getArgs  
            contents <- readFile fileName  
            putStrLn $ "The file has " ++ show (length (lines contents)) ++ " lines!"  

handler :: IOError -> IO ()  
handler e  
    | isDoesNotExistError e = putStrLn "The file doesn't exist!"  
    | otherwise = ioError e</pre><p>除了handler以外其他東西都沒變，我們只接住我們想要的I/O exception。這邊使用了<code>System.IO.Error</code>中的函數<strong>isDoesNotExistError</strong>跟<strong>ioError</strong>。<code>isDoesNotExistError</code>是一個運作在<code>IOError</code>上的predicate ，他代表他接受一個<code>IOError</code>然後回傳<code>True</code>或<code>False</code>，他的型態是<code>isDoesNotExistError :: IOError -> Bool</code>。我們用他來判斷是否這個錯誤是檔案不存在所造成的。我們這邊使用guard，但其實也可以用if else。如果exception不是由於檔案不存在所造成的，我們就用<code>ioEroror</code>重新丟出接到的exception。他的型態是<code>ioError :: IOException -> IO a</code>，所以他接受一個<code>IOError</code>然後產生一個會丟出exception的I/O action。那個I/O action的型態是<code>IO a</code>，但他其實不會產生任何結果，所以他可以被當作是<code>IO anything</code>。</p><p>所以有可能在<code>toTry</code>裡面丟出的exception並不是檔案不存在造成的，而<code>toTry `catch` handler</code>會接住再丟出來，很酷吧。</p><p>程式裡面有好幾個運作在<code>IOError</code>上的I/O action，當其中一個沒有被evaluate成<code>True</code>時，就會掉到下一個guard。這些predicate分別為：</p><ol><li><p><strong>isAlreadyExistsError</strong></p></li><li><p><strong>isDoesNotExistError</strong></p></li><li><p><strong>isFullError</strong></p></li><li><p><strong>isEOFError</strong></p></li><li><p><strong>isIllegalOperation</strong></p></li><li><p><strong>isPermissionError</strong></p></li><li><p><strong>isUserError</strong></p></li></ol><p>大部分的意思都是顯而易見的。當我們用了<strong>userError</strong>來丟出exception的時候，<code>isUserError</code>被evaluate成<code>True</code>。例如說，你可以寫<code>ioError $ userError "remote computer unplugged!"</code>，儘管用<code>Either</code>或<code>Maybe</code>來表示可能的錯誤會比自己丟出exception更好。</p><p>所以你可能寫一個像這樣的handler</p><pre class="code">handler :: IOError -> IO ()  
handler e  
    | isDoesNotExistError e = putStrLn "The file doesn't exist!"  
    | isFullError e = freeSomeSpace  
    | isIllegalOperation e = notifyCops  
    | otherwise = ioError e</pre><p>其中<code>notifyCops</code>跟<code>freeSomeSpace</code>是一些你定義的I/O action。如果exception不是你要的，記得要把他們重新丟出，不然你的程式可能只會安靜地當掉。</p><p><code>System.IO.Error</code>也會出了一些能詢問exception性質的函數，像是是哪些handle造成錯誤，或哪些檔案名造成錯誤。這些函數都是<code>ioe</code>當開頭。而且你可以在文件中看到一整串詳細資料。假設我們想要印出造成錯誤的檔案名。我們不能直接印出從<code>getArgs</code>那邊拿到的<code>fileName</code>，因為只有<code>IOError</code>被傳進handler中，而handler並不知道其他事情。一個函數只依賴於他所被呼叫時的參數。這也是為什麼我們會用<strong>ioeGetFileName</strong>這函數，他的型態是<code>ioeGetFileName :: IOError -> Maybe FilePath</code>。他接受一個<code>IOError</code>並回傳一個<code>FilePath</code>（他是<code>String</code>的同義詞。）基本上他做的事就是從<code>IOError</code>中抽出檔案路徑。我們來修改一下我們的程式。</p><pre class="code">import System.Environment     
import System.IO     
import System.IO.Error     
    
main = toTry `catch` handler     

toTry :: IO ()     
toTry = do (fileName:_) <- getArgs     
    contents <- readFile fileName     
    putStrLn $ "The file has " ++ show (length (lines contents)) ++ " lines!"     

handler :: IOError -> IO ()     
handler e     
    | isDoesNotExistError e =   
        case ioeGetFileName e of Just path -> putStrLn $ "Whoops! File does not exist at: " ++ path  
                                 Nothing -> putStrLn "Whoops! File does not exist at unknown location!"  
    | otherwise = ioError e</pre><p>在<code>isDoesNotExistError</code>是<code>True</code>的guard裡面，我們在case expression中用<code>e</code>來呼叫<code>ioeGetFileName</code>，然後用pattern matching拆出<code>Maybe</code>中的值。當你想要用pattern matching卻又不想要寫一個新的函數的時候，case expression是你的好朋友。</p><p>你不想只用一個<code>catch</code>來接你I/O part中的所有exception。你可以只在特定地方用<code>catch</code>接exception，或你可以用不同的handler。像這樣：</p><pre class="code">main = do toTry `catch` handler1  
          thenTryThis `catch` handler2  
          launchRockets</pre><p>這邊<code>toTry</code>使用<code>handler1</code>當作handler，而<code>thenTryThis</code>用了<code>handler2</code>。<code>launchRockets</code>並不是<code>catch</code>的參數，所以如果有任何一個exception被丟出都會讓我們的程式當掉，除非<code>launchRockets</code>使用<code>catch</code>來處裡exception。當然<code>toTry</code>，<code>thenTryThis</code>跟<code>launchRockets</code>都是I/O actions，而且被do syntax綁在一起。這很像其他語言中的try-catch blocks，你可以把一小段程式用try-catch包住，你可以自己調整該包多少進去。</p><p>現在你知道如何處裡I/O exception了。我們並沒有提到如何從pure code中丟出exception，這是因為正如我們先前提到的，Haskell提供了更好的辦法來處裡錯誤。就算是在可能會失敗的I/O action中，我也傾向用<code>IO (Either a b)</code>，代表他們是I/O action，但當他們被執行，他們結果的型態是<code>Either a b</code>，意思是不是<code>Left a</code>就是<code>Right b</code>。</p>
            <ul class="nav">
                <li class="left">  <img src="img/prv.png"></img><a href="make-types-and-classes-for-ourselves.html">構造我們自己的Types和Typeclasses</a></li>
                <li class="center"><a href="chapters.html">Index</a></li>
                <li class="right"> <a href="functionally-solving-problems.html">函數式地思考來解決問題</a><img src="img/nxt.png"></img></li>
            </ul>
        </div>
        <div id="footer">
        </div>
        <div id="feedback">Send feedback</div>
    </body>
</html>
